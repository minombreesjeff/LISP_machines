;;; -*- Syntax: Zetalisp; Mode: LISP; Package: SYSTEM-INTERNALS; Base: 10; Lowercase: T -*-
;;;>
;;;> *****************************************************************************************
;;;> ** (c) Copyright 1998-1982 Symbolics, Inc.  All rights reserved.
;;;> ** Portions of font library Copyright (c) 1984 Bitstream, Inc.  All Rights Reserved.
;;;>
;;;>    The software, data, and information contained herein are proprietary to,
;;;> and comprise valuable trade secrets of, Symbolics, Inc., which intends 
;;;> to keep such software, data, and information confidential and to preserve them
;;;> as trade secrets.  They are given in confidence by Symbolics pursuant 
;;;> to a written license agreement, and may be used, copied, transmitted, and stored
;;;> only in accordance with the terms of such license.
;;;> 
;;;> Symbolics, Symbolics 3600, Symbolics 3675, Symbolics 3630, Symbolics 3640,
;;;> Symbolics 3645, Symbolics 3650, Symbolics 3653, Symbolics 3620, Symbolics 3610,
;;;> Zetalisp, Open Genera, Virtual Lisp Machine, VLM, Wheels, Dynamic Windows,
;;;> SmartStore, Semanticue, Frame-Up, Firewall, Document Examiner,
;;;> Delivery Document Examiner, "Your Next Step in Computing", Ivory, MacIvory,
;;;> MacIvory model 1, MacIvory model 2, MacIvory model 3, XL400, XL1200, XL1201,
;;;> Symbolics UX400S, Symbolics UX1200S, NXP1000, Symbolics C, Symbolics Pascal,
;;;> Symbolics Prolog, Symbolics Fortran, CLOE, CLOE Application Generator,
;;;> CLOE Developer, CLOE Runtime, Common Lisp Developer, Symbolics Concordia,
;;;> Joshua, Statice, and Minima are trademarks of Symbolics, Inc.
;;;> 
;;;> Symbolics 3670, Symbolics Common Lisp, Symbolics-Lisp, and Genera are registered
;;;> trademarks of Symbolics, Inc.
;;;>
;;;> GOVERNMENT PURPOSE RIGHTS LEGEND
;;;> 
;;;>      Contract No.: various
;;;>      Contractor Name: Symbolics, Inc.
;;;>      Contractor Address: c/o Ropes & Gray
;;;> 			 One International Place
;;;> 			 Boston, Massachusetts 02110-2624
;;;>      Expiration Date: 2/27/2018
;;;>      
;;;> The Government's rights to use, modify, reproduce, release, perform, display or
;;;> disclose this software are restricted by paragraph (b)(2) of the "Rights in
;;;> Noncommercial Computer Software and Noncommercial Computer Software Documentation"
;;;> contained in the above identified contracts.  No restrictions apply after the
;;;> expiration date shown above.  Any reproduction of the software or portions thereof
;;;> marked with this legend must also reproduce the markings.  Questions regarding
;;;> the Government's rights may be referred to the AS&T Contracts Office of the
;;;> National Reconnaissance Office, Chantilly, Virginia 20151-1715.
;;;> 
;;;>      Symbolics, Inc.
;;;>      c/o Ropes & Gray
;;;>      One International Place
;;;>      Boston, Massachusetts 02110-2624
;;;>      781-937-7655
;;;>
;;;> *****************************************************************************************
;;;>

;;; Command Processor commands that provided any utilitarian functions,
;;; other than file manipulations, are included in this source, along
;;; with supporting routines; also, general utility routines for CP
;;; commands.  This file needs to be compiled and loaded before the
;;; other CP command sources, in system manufacture.

;;; Needed utility: a function to provide a fresh-line, and then a new
;;; line.  For a uniform single blank line between commands, regardless
;;; of their output.

(defun fresh-terpri (&optional (stream standard-output))
  (cl:fresh-line stream)
  (cl:terpri stream))

;;; Append command

(cp:define-command (com-append :command-table "File"
			       :provide-output-destination-keyword nil)
    ((sources
       '((cp::destination :direction :input))
       :prompt "sources"
       :confirm t
       :documentation "Sources to append together.")
     (destination
       '((cp::destination :direction :output))
       :prompt "forming"
       :confirm t)
;		    &KEY
;		    ;; what is this used for?
;		    (QUERY-EACH 'BOOLEAN :DEFAULT NIL)	;
;		    (ORDER
;		      '((CL:MEMBER
;			  :ALPHABETICAL :CHRONOLOGICAL
;			  :ASCENDING-VERSION :DESCENDING-VERSION
;			  :LEFT-TO-RIGHT
;			  ; :MYSTERIOUS ???
;			  ))
;		      :DEFAULT :LEFT-TO-RIGHT
;		      :DOCUMENTATION "Order in which to append these guys.")
     )
   (let ((destination-streams
	   (loop for stream in destination
		 collecting (cp::open-destination-stream stream :direction :output))))
     (with-open-stream (destination-stream 
			 (apply #'cl:make-broadcast-stream destination-streams))
       (loop for stream in sources
	     do
	 (with-open-stream (input-stream (cp::open-destination-stream stream
								      :direction :input))
	   (stream-copy-until-eof input-stream destination-stream)
	   (format destination-stream "~&")	;fresh-line
	   )))))


;;; Close commands

(cp:define-command (com-show-open-files :command-table "Debugging") ()
  (show-open-files)
  (values))

(defun show-open-files (&optional predicate)
  ;; I have a use for the predicate, which is why this is a separate function. -kmp 7-Sep-89
  (dolist (stream (fs:all-open-files))
    (let ((path (send stream :pathname)))
      (when (or (not predicate) (funcall predicate stream))
	(dw:with-output-as-presentation (:stream standard-output
					 :type 'sys:expression
					 :single-box t
					 :object stream)
	  (dw:with-output-as-presentation (:stream standard-output
					   :type 'fs:open-file
					   :single-box t
					   :object stream)
	    (let ((bytes (send-if-handles stream :read-pointer)))
	      (cond ((typep stream 'si:output-stream)
		     (format t "~&Output file:~12T~A~@[ (~D bytes written)~]" path bytes))
		    ((typep stream 'si:input-stream)
		     (let* ((length (cl:file-length stream))
			    (percent (when (and bytes length)
				       (if (zerop length)
					   100
					   (truncate (* bytes 100.) length)))))
		       (format t "~&Input file:~12T~A~:[~; (~:[~D bytes~;~D%~] read)~]"
			       path bytes percent (or percent bytes))))
		    (t
		     (let* ((length (cl:file-length stream))
			    (percent (when (and bytes length)
				       (truncate (* bytes 100.) length))))
		       (format t "~&File:~12T~A~:[~; (~:[~D bytes~;~D%~])~]"
			       path bytes percent (or percent bytes))))))))))))

(defun close-file-default ()
  (cond ((and (variable-boundp tv:who-line-file-state-sheet)
	      (instancep tv:who-line-file-state-sheet))
	 (let ((potential-stream
		 (symeval-in-instance tv:who-line-file-state-sheet 'tv:displayed-stream)))
	   (if (and (cl:streamp potential-stream)
		    ;; Don't consider stale data (closed streams)
		    (cl:member potential-stream (fs:all-open-files)))
	       potential-stream
	       :all)))
	(t :all)))

(cp:define-command (com-close-file :command-table "Debugging"
				   :provide-output-destination-keyword nil)
    ((file '((token-or-type (:all) fs:open-file))
	   :prompt "an open file"
	   :default (close-file-default)
	   :confirm t)
     &key
     (mode '((cl:member :abort :normal))
	   :documentation "Mode in which to close file."
	   ;; For files open for input, :abort and :normal are essentially the same.
	   ;; For files open for output, :normal should be used only if the user is
	   ;; sure the file contents were written successfully, or if the user is willing
	   ;; to take the responsibility for a half-written file.
	   :default :abort)
     (query-each 'boolean
		 :documentation "Whether to query before closing each file."
		 :default nil
		 :mentioned-default t)
     (report 'boolean
	     :documentation "Report each close as it occurs."
	     :default (not query-each)))
   (cl:case file
     (:all
       (fs:close-all-files   :mode mode :verbose report :query-each query-each))
     (otherwise
       (close-open-file file :mode mode :verbose report :query      query-each))))

(defun close-open-file (stream &key (mode :abort) (verbose t) (query nil))
  "Centralized subroutine for interactive closing of files."
  (check-arg mode (member mode '(:normal :abort)) ":NORMAL or :ABORT")
  (cl:loop
     (if (not query) (return))
     (cl:ecase (fquery (if (eq mode :abort)
			   '(:choices (((:yes "Yes")   #/Y #/Space)
				       ((:no  "No")    #/N #/Rubout)
				       ((:other "Other mode (normal)") #/O)))
			   '(:choices (((:yes "Yes")   #/Y #/Space)
				       ((:no  "No")    #/N #/Rubout)
				       ((:other "Other mode (abort)") #/O))))
		       "Going to close ~S in ~:[Normal~;Abort~] mode.~%Ok? "
		       stream (eq mode :abort))
       (:yes (return))
       (:no  (setq mode nil) (return))
       (:other (setq mode (if (eq mode :abort) :normal :abort)))))
  (cl:multiple-value-setq (mode verbose)
    (confirm-mode-for-close-open-file stream mode verbose))
  (cond (mode
	 (when verbose
	   (format error-output
		   "~&Closing ~S in ~:[Normal~;Abort~] mode... "
		   stream (eq mode :abort)))
	 (send stream :close (eq mode :abort))
	 (when verbose (format error-output "done.~%"))
	 (ncons stream))
	(verbose
	 (format query-io "~&Leaving ~S open.~%" stream)
	 nil)))

(defun confirm-mode-for-close-open-file (stream mode verbose)
  (cond ((or (neq mode :normal) 
	     (not (typep stream 'si:output-stream)))
	 (values mode verbose))
	((cl:yes-or-no-p
	   "D,#TD1PsT[Begin using 006 escapes](1 0 (NIL 0) (NIL :BOLD NIL) "CPTFONTCB")Warning:0 You are closing an output file in normal mode without the cooperation of~
          ~%  the program that opened it.  In doing so, you are indicating that all data was~
          ~%  written succesfully.  You should not do this unless either you are certain~
          ~%  that the file has in fact been fully written or else you are willing to take~
          ~%  the responsibility for a half-written file.~
          ~%Do you still want to close the file in normal mode? ")
	 (values :normal t))
	((cl:y-or-n-p "Close it in Abort mode? ") (values :abort t))
	(t (values nil t))))

;;; Create commands


;;; Disable commands

(cp:define-command (com-disable-network :command-table "Networks"
					:provide-output-destination-keyword nil)
    ((nets 'scl:((token-or-type			;† doesn't work right with this
		   (:All)
		   ((sequence neti:local-network))))
	   :prompt "network(s)"
	   :documentation "local networks to disable"
	   :default :all))
   (unless (listp nets)
     (setq nets (list nets)))
   (when (eq (first nets) :all)
     (setq nets
	   neti:*local-networks*))
   (dolist (net nets)
     (format t "~&Disabling network ~A." net)
     (send net :disable)))

(define-presentation-type neti:service ((&key enabled-p))
   :parser ((stream &key type initially-display-possibilities)
	    (dw:completing-from-suggestions (stream :type type
						    :initially-display-possibilities
						    initially-display-possibilities)
	      (loop for server in neti:*servers*
		    as protocol-name = (neti:server-protocol-name server)
		    when (eq enabled-p (not (null (neti:service-enabled-p protocol-name))))
		      do
			(dw:suggest (string protocol-name) protocol-name))))
   :describer ((stream &key plural-count)
	       (when (numberp plural-count)
		 (setq plural-count (> plural-count 1)))
	       (cond (enabled-p
		      (if plural-count
			  (cl:write-string "enabled services" stream)
			  (cl:write-string "an enabled service" stream)))
		     (t
		      (if plural-count
			  (cl:write-string "disabled services" stream)
			  (cl:write-string "a disabled service" stream))))))


(cp:define-command (com-disable-services :command-table "Networks"
					 :provide-output-destination-keyword nil)
    ((services '((token-or-type (:all) ((cl:sequence ((neti:service :enabled-p t))))))
	       :default :all
	       :prompt "enabled services"
	       :documentation "Services to disable"))
   (disable-services services t))


;;; Enable commands


(cp:define-command (com-enable-network :command-table "Networks"
				       :provide-output-destination-keyword nil)
		   ((nets 'scl:((token-or-type	;† doesn't work right with this
				 (:All)
				 ((sequence neti:local-network))))
			  :prompt "network(s)"
			  :documentation "local networks to enable"
			  :default :all))
  (unless (listp nets)
    (setq nets (list nets)))
  (when (eq (first nets) :all)
    (setq nets
	  neti:*local-networks*))
  (dolist (net nets)
    (format t "~&Enabling network ~A." net)
    (send net :enable)))


(cp:define-command (com-enable-services :command-table "Networks"
					:provide-output-destination-keyword nil)
    ((services '((token-or-type (:all) ((cl:sequence neti:service))))
	       :default neti:*standard-services-enabled*
	       :prompt "disabled services"
	       :documentation "Services to enable"))
   (enable-services services t))


;;; Halt commands

(cp:define-command (com-halt-gc :name "Halt GC" :provide-output-destination-keyword nil
				:command-table "Garbage Collection")
    ()
   (gc-off))


#-VLM
(cp:define-command (com-halt-machine :provide-output-destination-keyword nil
				     :command-table "Session")
    (&key (query 'scl:boolean :default t :documentation "Whether to query for confirmation."))
   (block skip
     (when (or (not query) (yes-or-no-p "Do you really want to halt the machine? "))
       (cond ((remote-terminal-p)
	      (unless (or (not query)
			  (cl:yes-or-no-p
			    "~&You are connected to a remote terminal.~
			     ~%Halting the machine will affect processes connected to~
			     ~%other remote terminals, as well as the main console.~
			     ~%It will not be possible for you to continue remotely.~
			     ~%Are you sure you still want to halt the machine? "))
		(format t "~&Not halting the machine.~%")
		(return-from skip)))
	     (t
	      (format t "~&To continue running, use the Continue command.~%")))
       (halt))))

#+VLM
(defvar *halt-genera-shutdown-default* t
  "If NIL, the Halt Genera command will not exit the genera application;
  you will be able to resume Genera by typing :Start at the cold-load stream.
  If T, the Halt Genera command will exit the genera application; you will
  not be able to resume.")

#+VLM
(cp:define-command (com-halt-genera :command-table "Session"
				     :provide-output-destination-keyword nil)
    (&key
      (query '((scl:alist-member :alist (("Yes" t) :confirm-only ("No" nil))))
	     :mentioned-default :confirm-only
	     :default t
	     :documentation
	     "Whether to ask about each decision, just confirm, or don't ask")
      (delay '((null-or-type time:time-interval))
	     :when (fboundp 'fs:file-server-shutdown)
	     :prompt "time"
	     :default (if net:*active-servers* 300. nil)	;5 mins if servers exist.
	     :documentation "How long to delay before halting.")
      (logout 'boolean
	      :default *halt-genera-shutdown-default*
	      :documentation "Log out before halting.")
      (shutdown '((scl:alist-member
		    :alist (("Yes" :value t
			     :documentation
			     #-VLM "Stop the FEP"
			     #+VLM "Exit the genera application")
			    ("No" :value nil
			     :documentation
			     #-VLM "Remain in the FEP"
			     #+VLM "Remain in the VLM Debugger"))))
		:default *halt-genera-shutdown-default*
		:documentation
		#-VLM
		"Whether to shut down after halting."
		#+VLM
		"Whether to exit the genera application after halting."
		)
      (reason '((null-or-type string))
	      :prompt "for halting"
	      :default NIL
	      :documentation "Used to warn active clients about pending halt.")
      (simulate 'boolean
		:default nil
		:mentioned-default t
		:documentation
		"Whether to just show the effect of this command without actually doing it."))
   "Halts Genera, optionally exiting the genera application."
   (if (not delay) (setq delay 0))
   (let ((confirmation
	   (format nil "Do you really want to halt ~:[~*~;and ~:[shutdown ~;exit, ~'pirrecoverably losing all state~,~%  ~]~]~[immediately~:;in ~:*~\time-interval\~]~:[, ~'iwithout~ logging out~;~]? "
		   shutdown #+VLM t #-VLM nil delay logout)))
    (when (and simulate query)
      (format t "~&The following query would appear:~
		      ~%  ~~A~~
		      ~%Only if you responded /"Yes/" would execution proceed ~
			beyond this point."
	      confirmation))
   (when (or simulate (null query) (yes-or-no-p confirmation))
     (if (eq query :confirm-only) (setq query nil))
     (when (remote-terminal-p)
       (unless (or (not query)
		   (cl:yes-or-no-p
		     "~&You are connected to a remote terminal.~
		      ~%Halting the machine will affect processes connected to~
		      ~%other remote terminals, as well as the main console.~
		      ~%It will not be possible for you to continue remotely.~
		      ~%Are you sure you still want to halt the machine? "))
	 (format t "~&Not halting the machine.~%")
	 (return-from com-halt-genera)))
     (let ((fep-command-string (and shutdown
				    (format nil ":Shutdown~:[~; :Query No~]~%"
					    (not query)))))
       (cp::halt-machine-internal
	 :final-message
	 (and (not shutdown)
	      #-VLM "~&To continue running, use the FEP Start command.~%"
	      #+VLM "~&To continue running, use the VLM Debugger :Start command.~%")
	 :logout logout
	 :delay delay
	 :query query
	 :reason reason
	 :fep-command-string fep-command-string
	 :description #-VLM "halt" #+VLM (if shutdown "exit" "halt")
	 :simulate simulate)))))


;;; Initialize commands

(cp:define-command (com-initialize-mouse :provide-output-destination-keyword nil
					 :command-table "Window")
    (
;		    &key
;		     (Sheet :string
;			    :default tv:default-screen
;			    :documentation "Window in which to initialize the mouse")
     )
   ;; What does :sheet mean here, anyway?
   (tv:mouse-initialize))

;;; Log commands

(defvar *most-recent-login-terminal* nil)

(add-initialization "Preset remote terminal auto-logout"
		    `(setq *most-recent-login-terminal* cl:*terminal-io*)
		    nil 'si:login-initialization-list)

(add-initialization "Reset remote terminal auto-logout"
		    `(setq *most-recent-login-terminal* nil)
		    nil 'si:logout-initialization-list)

(cp:define-command (com-login :provide-output-destination-keyword nil
			      :command-table "Session")
		   ((user '((scl:type-or-string net:user))
			  :prompt "user name"
			  :provide-default nil
			  :documentation "User name under which to login")
		    &key
		    (host 'net:host
			  :default nil
			  :mentioned-default (unless (stringp user)
					       (send user :home-host))
			  :provide-default (unless (stringp user) t)
			  :documentation "Host to serve as login host")
		    (init-file '((scl:token-or-type
				   (:none :default-init-file)
				   ((cl:pathname) :default-type nil :description "file")))
			       :default (if (eq user neti:*sys-host-user*)
					    :none
					    :default-init-file)
			       :documentation "File to load as user's initialization file"))
   (unless (let ((string (logout-check-last-login-location)))
	     (or (not string)
		 (cl:yes-or-no-p "~A~%Do you really want to proceed? " string)))
     (format t "~&~%  Login was not attempted.")
     (return-from com-login))
   (let (load-init-file init-file-to-load)
     (when (or (null user) (string-equal user ""))
       (scl:destructuring-bind (id &key ((:host h)) ((:init-file i)))
	   (cp:read-command-arguments 'com-login)
	 (setq user id
	       host h
	       init-file i)))
     (unless (eq init-file ':none) (setq load-init-file t))
     (when (cl:typep init-file 'cl:pathname) (setq init-file-to-load init-file))
     (login user :host host :load-init-file load-init-file
	    :init-file-to-load init-file-to-load)))

(defvar *application-logout-keywords* ()
  :documentation "added to by define-application-logout-initialization")

(defvar *application-logout-values* ()
  :documentation "bound by :Logout command for benefit of application-logout initializations")

(defvar *application-logout-query-results* ()
  :documentation "bound by :Logout command to batch results of application-logout queries")

;;; controls for separate-querying application logouts
(defvar *application-logout-query-phase*)				;run time
(defvar *application-logout-separate-query-name*)			;compile time
(defvar *application-logout-compile-time-separate-querying-was-indeed-used*)	;compile time

(defmacro define-application-logout-initialization (name logout-command-keywords-and-ptypes
						    &body body)
  (let
    ((record-source-file-name-list
       (scl:map 'list #'(lambda (keyword-and-ptype)
			  `(record-source-file-name
			     ',(intern (string (car keyword-and-ptype))
				       (scl:find-package "keyword"))
			     'application-logout-keyword))
		logout-command-keywords-and-ptypes))
     (local-variable-list
       (scl:map 'list #'(lambda (keyword-and-ptype)
			  `(,(car keyword-and-ptype)
			    (retrieve-application-logout-keyword-value
			      ',(car keyword-and-ptype))))
		logout-command-keywords-and-ptypes))
     (pushnew-list
       (scl:map 'list #'(lambda (keyword-and-ptype)
			  `(scl:pushnew ',(cons (intern (string (car keyword-and-ptype))
							(scl:find-package "keyword"))
						(cdr keyword-and-ptype))
					*application-logout-keywords*
					:key #'first
					:replace t))
		logout-command-keywords-and-ptypes)))
    `(when (and ,@record-source-file-name-list)
      (defun ,name ()
	(compiler-let ((*application-logout-separate-query-name* ',name)
		       (*application-logout-compile-time-separate-querying-was-indeed-used* nil))
	  (multiple-value-prog1
	    (let ((results (multiple-value-list
			     (catch 'separated-query-values
			       (let
				 ,local-variable-list
				 ,@body)))))
	      (when *application-logout-query-phase*
		(store-application-logout-query-result ',name results))
	      (apply #'values results))
	    (check-that-compile-time-separate-querying-was-indeed-used))))
       (add-initialization ',name '(,name) () 'application-logout)
      ,@pushnew-list)))

(defun retrieve-application-logout-keyword-value (key)
  (getf *application-logout-values*
	(intern (string key) (scl:find-package "keyword"))))

;;; Helpers for application logout spearated querying
(defun store-application-logout-query-result (name result)
  (scl:pushnew (cons name result) *application-logout-query-results* :key #'first :replace t))

(defun retrieve-application-logout-query-result (name)
  (cdr (scl:find name *application-logout-query-results* :key #'first)))

;; compile time
(defmacro application-logout-separate-querying (&body body)
  (setq *application-logout-compile-time-separate-querying-was-indeed-used* T)
  `(application-logout-separate-querying-1
     (sys:named-lambda application-logout-separate-querying ()
       (declare (sys:downward-function))
       ,@body)
     ',*application-logout-separate-query-name*))

(defmacro check-that-compile-time-separate-querying-was-indeed-used ()
  (unless *application-logout-compile-time-separate-querying-was-indeed-used*
    (warn "~s body failed to call ~@
           ~s.~@
           You should use ~:*~s to allow the execution of~@
           your application logout function to be done after the query answers from ALL~@
           application logouts have been gathered."
	  'define-application-logout-initialization 'application-logout-separate-querying)))

;; run time
(defun application-logout-separate-querying-1 (continuation name)
  (if *application-logout-query-phase*
      (throw 'separated-query-values (funcall continuation))
      (recover-separated-query-values name)))

(defun recover-separated-query-values (name)
  (apply #'values (retrieve-application-logout-query-result name)))

(defun logout-check-remote-terminals ()
  (let ((remotes (without-interrupts
		   (loop for window being the array-elements
			     of tv:previously-selected-windows
			 when (and window
				   (flavor:find-flavor 'telnet:nvt-window nil)
				   (typep window 'telnet:nvt-window)
				   (send window :connected-p))
			   collect window))))
    (when remotes
      (format nil "You still have ~R active terminal~:P." (length remotes)))))

(defun logout-check-last-login-location ()
  (unless (or ;; If user is not logged in, proceed.
	      (eq *user* *not-logged-in-user*)
	      ;; If user is on the main screen, proceed.
	      (not (sys:remote-terminal-p cl:*terminal-io*))
	      ;; If user previously logged in on this console, proceed.
	      (eq *most-recent-login-terminal* cl:*terminal-io*))
    (format nil "~A is logged in from another terminal." *user*)))

(defvar *confirm-logout-p* t)

(defvar *logout-confirmations* '(logout-check-remote-terminals
				 logout-check-last-login-location))

(defun confirm-logout (&key (verb "logout") (simulate nil))
  (or (not *confirm-logout-p*)
      (let ((things-to-confirm
	      (mapcan #'(lambda (function)
			  (let ((result (funcall function)))
			    (when result (ncons result))))
		      *logout-confirmations*)))
	(or (not things-to-confirm)
	    (let ((message (format nil "~{~A~%~}Do you really want to ~A? "
				   things-to-confirm verb)))
	      (if simulate
		  (progn (format t "~&The following query would appear:~
		                    ~%  ~~A~~
		                    ~%Only if you responded /"Yes/" would execution proceed ~
			              beyond this point."
				 message)
			 t)
		  (let ((result (cl:yes-or-no-p "~A" message)))
		    (unless result (format t "~&Not going to ~A.~%" verb))
		    result)))))))

(cp:define-command  (com-logout :command-table "Session"
				  :provide-output-destination-keyword nil)
    ((key-val-pairs `((dw::keyword-value-pairs ,*application-logout-keywords*))
		    :display-default nil
		    :provide-default t
		    :default '()
		    :prompt "keywords"))
   (unless (confirm-logout) (return-from com-logout))
   (let ((*application-logout-values* (dw::merge-keyword-value-pairs
					key-val-pairs
					*application-logout-keywords*))
	 (*application-logout-query-results* ()))
     (let ((*application-logout-query-phase* t))
       (initializations 'application-logout :redo-flag t)	; do the queries
       (setq *application-logout-query-phase* nil)
       (initializations 'application-logout :redo-flag t)))	; then execute on the answers
   (zl:logout)
   (format t "~&~%  Logged out."))



;;; Reset commands

(cp:define-command (com-reset-network :provide-output-destination-keyword nil
				      :command-table "Networks")
    ()
   (neti:general-network-reset)
   )

#||

(cp:define-command (com-reset-file-host
		     :command-table "Networks"
		     :provide-output-destination-keyword nil)
    ((hosts `((cl:sequence
		((cl:member ,@(loop for host in fs:*pathname-host-list*
				    when (send host ':peek-file-system-p)
				      collect host))
		 :description "a file server in use")))
	    :prompt "names"
	    :default (list fs:user-login-machine)
	    :confirm t
	    :documentation "Names of hosts with file access paths to be reset."
	    :provide-default (and (typep fs:user-login-machine 'net:host)
				  (loop for host in fs:*pathname-host-list*
					when (send host ':peek-file-system-p)
					  thereis (eq host fs:user-login-machine)))))
   (dolist (host hosts)
     (send host :file-reset t)))

||#



(cp:define-command (com-set-time :command-table "Session"
				 :provide-output-destination-keyword nil)
    ((new-time '((scl:token-or-type (("Network" :value :network
				      :documentation "Get time from network broadcast")
				     ("Calendar clock" :value :calendar-clock
				      :documentation "Get time from calendar clock or embedding host"))
				    ((time:universal-time) :present-based t)))
	       :default (time:get-universal-time) :confirm t
	       :prompt "new time or special source"
	       :documentation "New time to set")
     &key
     (set-status-line '((scl:alist-member :alist (("Yes" . t) ("No" . nil) ("Ask" . :ask))))
		      :default :ask :mentioned-default t)
     (set-calendar-clock '((scl:alist-member :alist (("Yes" . t) ("No" . nil) ("Ask" . :ask))))
			 :default (if (eq new-time :calendar-clock) nil :ask)
			 :mentioned-default t))
   (cl:case new-time
     (:network
       (setq new-time (net:get-time-from-network)))
     (:calendar-clock
       (setq new-time (time:read-calendar-clock))))
   (when (null new-time)
     (setq new-time (accept '((time:universal-time) :present-based t)
			    :prompt "Please specify the time")))
   (format t "~&New time is ~\TIME\.~%" new-time)
   (when (eq set-status-line :ask)
     (setq set-status-line (cl:y-or-n-p " Set status line time? ")))
   (when (eq set-calendar-clock :ask)
     (setq set-calendar-clock (cl:y-or-n-p
				" Set calendar clock~:[ (host time)~]~:[ as well~]? "
				(not #+3600 nil
				     #+imach (sys:system-case (embedded t) (otherwise nil)))
				(not set-status-line))))
   (when set-status-line
     (time:initialize-timebase new-time nil))
   (when set-calendar-clock
     (condition-case (error)
	  (time:set-calendar-clock new-time)
	(error
	  (format error-output "~&Error setting calendar clock: ~~A~" error)
	  (setq set-calendar-clock nil))))
   (if (or set-status-line set-calendar-clock)
       (format t "~&Time set in ~:[status line~]~:[ and ~]~:[calendar clock~].~%"
	       (not set-status-line)
	       (not (and set-status-line set-calendar-clock))
	       (not set-calendar-clock))
       (format t "~&Time not set.~%")))

(cp:define-command (com-show-time :command-table "Utilities") ()
   "Shows the time of day."
   (format t "~&It is now ~Q.~%"
	   #'(lambda () (show-time :show-seconds nil)))
   (values))

(defun show-time (&key (universal-time (cl:get-universal-time))
		          (stream cl:*standard-output*) (mode :long)
			  (show-date t) (show-day show-date) (show-time t) (show-seconds t)
			  (date-time-separator " "))
  (multiple-value-bind (sec min hr date mon yr day daylight-p timezone)
      (cl:decode-universal-time universal-time)
    (let ((hour (cond ((zerop hr) 12.)
		      ((<= hr 12.) hr)
		      (t (- hr 12.)))))
      (format stream
	      "~:[~*~;~A~]~:[~;, ~]~:[~3*~;~A ~D, ~D~]~:[~*~;~A~]~
	       ~:[~6*~;~D:~2,'0D~@[:~2,'0D~]~A~@[~A~] ~A~]"
	      show-day (time:day-of-the-week-string day mode)
	      (and show-day (or show-time show-date))
	      show-date (time:month-string mon mode) date
	      (if (eq mode :short) (remainder yr 100) yr)
	      (and show-time show-date) date-time-separator
	      show-time hour min (if (and show-seconds (not (= sec 0))) sec)
	      (cond ;; Mentioning midnight would just confuse people about which
		    ;; day was being referred to.
		    ((< hr 12.) "am")
		    (t "pm"))
	      (unless (eq mode :short)
		(cond ((and (= hr 12.) (= min 0.) (or (not show-seconds) (= sec 0)))
		       " (noon)")
		      ((and (= hr 0.) (= min 0.) (or (not show-seconds) (= sec 0)))
		       " (midnight)")))
	      (time:timezone-string timezone daylight-p)))))


;;; Do we really want this command?
;(define-cp-command (com-set-user-id :name "Set User ID")
;		   ((user-name 'string
;			       :default user-id
;			       :documentation "Name to set in user-id"))
;  (setq user-id user-name)
;  (format t "~& User ID set to /"~A/"" user-name))


;;; Show commands

(cp:define-command (com-show-login-history :command-table "World Building")
    ;; No need to supply the output-destination keyword
    ;; until we can do table formatting to files and buffers...
    ()
   (si:show-login-history))

;;; Start commands

(cp:define-command (com-start-gc :name "Start GC" :command-table "Garbage Collection"
				 :provide-output-destination-keyword nil)
		   (&key
		     (dynamic '((cl:member :yes :no))
			      :mentioned-default :yes
			      :documentation "Dynamic level of incremental GC")
		     (ephemeral '((cl:member :yes :no))
				:mentioned-default :yes
				:documentation "Ephemeral level of incremental GC")
		     (immediately '((cl:member :yes :no :in-place :by-area))
				  :mentioned-default :yes
				  :documentation "Perform a complete garbage collection now")
		     (selective '((cl:member :yes :no))
				:mentioned-default :yes
				:documentation "Specify areas in which to collect garbage")
		     (cleanup '((cl:member :yes :no :ask))
			      :mentioned-default :yes
			      :documentation "Perform a GC Cleanup before the garbage collection"))
  (when (eq immediately :no)
    (setq immediately nil))
  (when (eq selective :no)
    (setq selective nil))
  (when (eq immediately :by-area)
    (setq selective :yes
	  immediately :yes))
  (unless (or dynamic ephemeral immediately cleanup selective)
    (setq dynamic :yes ephemeral :yes))		;none mentioned means dynamic&ephemeral
  (setq dynamic (selectq dynamic
		  (:yes t)
		  (:no nil)
		  (otherwise (get (locf gc-on) :dynamic))))  ;unmentioned means don't change
  (setq ephemeral (selectq ephemeral
		    (:yes t)
		    (:no nil)
		    (otherwise (get (locf gc-on) :ephemeral))))
  (when (and immediately (not cleanup))
    (setq cleanup
	  (accept '((scl:alist-member :alist (("Yes" :yes) ("No" nil) ("Ask" :ask))))
		  :prompt "Perform a GC Cleanup before starting the garbage collector? "
		  :default :ask)))
  (cl:case cleanup
    (:ask (run-gc-cleanups t))
    (:yes (run-gc-cleanups)))
  (cl:case immediately
    (:yes      (if selective
		   (gc-immediately-by-area)
		   (gc-immediately)))
    (:in-place (if selective
		   (gc-in-place-by-area)
		   (gc-in-place))))
  (when (and selective (not immediately))
    (format t "~&The :Selective option to ~\cp:command\ is only meaningful~@
		 in conjunction with the :Immediately option." '(com-start-gc)))
  (gc-on :dynamic dynamic :ephemeral ephemeral)
  (format t "~& Ephemeral Garbage Collector is ~:[Off~;On~],  ~
                Dynamic Garbage Collector is ~:[Off~;On~]."
	  (get (locf gc-on) :ephemeral) (get (locf gc-on) :dynamic)))

(cp:define-command (com-set-gc-options :name "Set GC Options"
				       :command-table "Garbage Collection"
				       :provide-output-destination-keyword nil)
		   ()
  (choose-gc-parameters))

(cp:define-command (com-optimize-world :provide-output-destination-keyword nil
				       :command-table "World Building")
		   (&key
		    (show 'boolean :default nil :mentioned-default t
		     :documentation "Show internal details of the operation as it proceeds"))
   (when (yes-or-no-p "This operation will take approximately forty minutes.  During that
time the machine will not respond to the keyboard nor to the network.
Do you really want to optimize the world? ")
     ;; We should make more of these options available...
     (si:immediate-gc :mode :layered-ids-release :verbose show)
     (format t "~&Finished optimizing world.~%")))


;; Use define-command-and-parser here rather than define-command
;; so I can put a FLET inside the parser body.
#-VLM
(cp:define-command-and-parser (com-add-paging-file :name "Add Paging File"
						   :command-table "Session"
						   :provide-output-destination-keyword nil)
			      (pathnames &key prepend)
  (flet ((fep-file-on-local-host-p (pathname)
	   (ignore-errors
	     (eq (send (send pathname :host) :host) net:*local-host*))))
    (cp::assign-argument-value
      pathnames
      (cp::read-command-argument `((scl:sequence
				     ((and fs:fep-pathname
					   ((cl:satisfies ,#'fep-file-on-local-host-p)))
				      :description "a local FEP pathname")))
				 :query-identifier 'pathname
				 :prompt "New paging file(s)"
				 :default (ncons (fs:parse-pathname "FEP:>.Page"))
				 :confirm t))
    (cp::read-keyword-arguments
      (prepend 'scl:boolean
	       :prompt "Prepend"
	       :default nil)))
  (multiple-value-bind (old-dynamic-size old-static-size old-free-size old-old-size)
      (gc-get-space-sizes)
    (ignore old-dynamic-size old-static-size old-old-size)
    (loop for pathname in pathnames do
      (condition-case ()
	   (unless (si:add-paging-file pathname prepend)
	     (format t "~&Paging file ~A not added." pathname))
	 (fs:fep-file-swap-locked
	   (format error-output "~&File ~A is already in use as a paging file."
		   pathname))
	 (fs:file-not-found
	   (format error-output "The file ~A does not exist." pathname))
	 (:no-error
	   (format t "~&Added paging file ~A." pathname))))
    (multiple-value-bind (dynamic-size static-size free-size old-size)
	(gc-get-space-sizes)
      (ignore dynamic-size static-size old-size)
      (format t "~&Available space up ~:D from ~:D to ~:D.~%"
	      (- free-size old-free-size) old-free-size free-size))))

;;; Chaos screen spy user side UI.

(defvar *monitor-screen-default-host* nil)

(cp:define-command (com-monitor-screen :command-table "UTILITIES"
				       :provide-output-destination-keyword nil)
    ((machine 'net:host
	      :default (or *monitor-screen-default-host*
			   neti:*local-host*)
	      :prompt "of cooperating host"
	      :confirm t))
   (setq *monitor-screen-default-host* machine)
   (condition-case (err)
	(chaos:screen-spy machine)
      (error
	(send err :report standard-output)))
   (values))
