;;; -*- Mode: Lisp; Package: System-Internals; Base: 8 -*-
;;;>
;;;> *****************************************************************************************
;;;> ** (c) Copyright 1998-1982 Symbolics, Inc.  All rights reserved.
;;;> ** Portions of font library Copyright (c) 1984 Bitstream, Inc.  All Rights Reserved.
;;;>
;;;>    The software, data, and information contained herein are proprietary to,
;;;> and comprise valuable trade secrets of, Symbolics, Inc., which intends 
;;;> to keep such software, data, and information confidential and to preserve them
;;;> as trade secrets.  They are given in confidence by Symbolics pursuant 
;;;> to a written license agreement, and may be used, copied, transmitted, and stored
;;;> only in accordance with the terms of such license.
;;;> 
;;;> Symbolics, Symbolics 3600, Symbolics 3675, Symbolics 3630, Symbolics 3640,
;;;> Symbolics 3645, Symbolics 3650, Symbolics 3653, Symbolics 3620, Symbolics 3610,
;;;> Zetalisp, Open Genera, Virtual Lisp Machine, VLM, Wheels, Dynamic Windows,
;;;> SmartStore, Semanticue, Frame-Up, Firewall, Document Examiner,
;;;> Delivery Document Examiner, "Your Next Step in Computing", Ivory, MacIvory,
;;;> MacIvory model 1, MacIvory model 2, MacIvory model 3, XL400, XL1200, XL1201,
;;;> Symbolics UX400S, Symbolics UX1200S, NXP1000, Symbolics C, Symbolics Pascal,
;;;> Symbolics Prolog, Symbolics Fortran, CLOE, CLOE Application Generator,
;;;> CLOE Developer, CLOE Runtime, Common Lisp Developer, Symbolics Concordia,
;;;> Joshua, Statice, and Minima are trademarks of Symbolics, Inc.
;;;> 
;;;> Symbolics 3670, Symbolics Common Lisp, Symbolics-Lisp, and Genera are registered
;;;> trademarks of Symbolics, Inc.
;;;>
;;;> GOVERNMENT PURPOSE RIGHTS LEGEND
;;;> 
;;;>      Contract No.: various
;;;>      Contractor Name: Symbolics, Inc.
;;;>      Contractor Address: c/o Ropes & Gray
;;;> 			 One International Place
;;;> 			 Boston, Massachusetts 02110-2624
;;;>      Expiration Date: 2/27/2018
;;;>      
;;;> The Government's rights to use, modify, reproduce, release, perform, display or
;;;> disclose this software are restricted by paragraph (b)(2) of the "Rights in
;;;> Noncommercial Computer Software and Noncommercial Computer Software Documentation"
;;;> contained in the above identified contracts.  No restrictions apply after the
;;;> expiration date shown above.  Any reproduction of the software or portions thereof
;;;> marked with this legend must also reproduce the markings.  Questions regarding
;;;> the Government's rights may be referred to the AS&T Contracts Office of the
;;;> National Reconnaissance Office, Chantilly, Virginia 20151-1715.
;;;> 
;;;>      Symbolics, Inc.
;;;>      c/o Ropes & Gray
;;;>      One International Place
;;;>      Boston, Massachusetts 02110-2624
;;;>      781-937-7655
;;;>
;;;> *****************************************************************************************
;;;>

;; Lisp Command Loop

;; These are variables which the user can set to control the behavior of all the lisp command
;; loops in the system which do not provide their own values for these parameters.
;; *COMMAND-LOOP-READ-FUNCTION* and *COMMAND-LOOP-BREAK-READ-FUNCTION* are hooks for the CP.

(DEFVAR *COMMAND-LOOP-ABORT-FUNCTION* 'COMMAND-LOOP-ABORT-FUNCTION)
(DEFVAR *COMMAND-LOOP-READ-FUNCTION* 'READ-FORM)
(DEFVAR *COMMAND-LOOP-EVAL-FUNCTION* 'EVAL)
(DEFVAR *COMMAND-LOOP-PRINT-FUNCTION* 'COMMAND-LOOP-PRINT-FUNCTION)

(DEFVAR *COMMAND-LOOP-BREAK-READ-FUNCTION* 'COMMAND-LOOP-BREAK-READ-FUNCTION)
(DEFVAR *COMMAND-LOOP-BREAK-EVAL-FUNCTION* 'EVAL)
(DEFVAR *COMMAND-LOOP-BREAK-PRINT-FUNCTION* 'COMMAND-LOOP-PRINT-FUNCTION)

(DEFUN COMMAND-LOOP-ABORT-FUNCTION (NAME)
  (FORMAT T "Back to ~A in ~A." NAME TERMINAL-IO))

;; Implement the PRIN1 hack for backward compatibilty only.  Full control of how all of the
;; values are printed may be achieved by setting *COMMAND-LOOP-PRINT-FUNCTION*

(DEFUN COMMAND-LOOP-PRINT-FUNCTION (VALUES)
  (SEND STANDARD-OUTPUT :FRESH-LINE)
  (DOLIST (VALUE VALUES)
    (FUNCALL (OR PRIN1 #'PRIN1) VALUE)
    (SEND STANDARD-OUTPUT :FRESH-LINE)))

;; Only bind +, *, etc. if they are not already bound in the current process.  If they are
;; already bound, then share them with the next command loop up since that gives them
;; per-stream behavior.  If they are not already bound, then bind them so that the global
;; values aren't modified.

(DEFVAR-RESETTABLE *COMMAND-LOOP-VARIABLE-BINDINGS* NIL)

(DEFCONST *COMMAND-LOOP-VARIABLE-BINDINGS-BINDINGS*
	  '((+ NIL) (++ NIL) (+++ NIL)
	    (* NIL) (** NIL) (*** NIL)
	    (// NIL) (//// NIL) (////// NIL)
	    (- NIL)
	    (*COMMAND-LOOP-VARIABLE-BINDINGS* T)))

(DEFMACRO WITH-COMMAND-LOOP-VARIABLE-BINDINGS (&BODY BODY)
  `(PROGW (AND (NOT *COMMAND-LOOP-VARIABLE-BINDINGS*)
	       *COMMAND-LOOP-VARIABLE-BINDINGS-BINDINGS*)
     . ,BODY))

(DEFCONST *COMMAND-LOOP-STREAM-BINDINGS-BINDINGS*
	  '((STANDARD-INPUT  SYN-TERMINAL-IO)
	    (STANDARD-OUTPUT SYN-TERMINAL-IO)
	    (ERROR-OUTPUT    SYN-TERMINAL-IO)
	    (QUERY-IO        SYN-TERMINAL-IO)
	    (DEBUG-IO        SYN-TERMINAL-IO)
	    #|(TRACE-OUTPUT    SYN-TERMINAL-IO)|#))	;trace should stay the same

(DEFMACRO WITH-COMMAND-LOOP-STREAM-BINDINGS (&BODY BODY)
  `(PROGW *COMMAND-LOOP-STREAM-BINDINGS-BINDINGS*
     . ,BODY))



(DEFVAR TV:INITIAL-LISP-LISTENER)
(DEFVAR *SIMPLE-LISTENER-PROCESS*)

(DEFF INITIALIZE-APPLICATION 'INITIALIZE-LISP-LISTENER)

;; Come here when machine starts.
(DEFUN LISP-TOP-LEVEL-WARM ()
  (SETQ TV:*FORCIBLY-SHOW-PROGRESS-NOTES* T)	;Let user see what's happening
  (LISP-REINITIALIZE-PART-1)			;(Re)Initialize critical variables and things
  (PROCESS:MAKE-PROCESS
    "Initialize Genera"
    :INITIAL-FUNCTION #'(LAMBDA ()
			  (RUN-GENERA-INITIALIZATIONS)
			  (INITIALIZE-APPLICATION)
			  (LISP-REINITIALIZE-PART-2))
    :FLAGS '(:SYSTEM-PROCESS T :NOT-RESTARTABLE T :NO-BACKGROUND-INTERACTOR T))
  (PROCESS:RESET-AND-RELEASE-RESOURCES *CURRENT-PROCESS*))

(DEFUN RUN-GENERA-INITIALIZATIONS ()
  (SETF TV:WHO-LINE-PROCESS *CURRENT-PROCESS*)
  (FS:WITH-AUTOMATIC-LOGIN-TO-SYS-HOST
    ;; These are initializations that have to be done before other initializations
    (INITIALIZATIONS 'SYSTEM-INITIALIZATION-LIST T)
    (WHEN (NOT *WARM-BOOTED-P*)
      (INITIALIZATIONS 'COLD-INITIALIZATION-LIST T))
    (DISABLE-SERVICES)
    (NETI:INITIALIZE-NAMESPACES-AND-NETWORK)
    (INITIALIZATIONS 'WARM-INITIALIZATION-LIST T)))

(DEFUN FIND-OR-CREATE-INITIAL-LISP-LISTENER (SCREEN &OPTIONAL (SPECIAL-MARGIN-COMPONENTS T))
  ;; First look for a precreated window that is on the desired screen or can be put onto it
  #+VLM
  (WHEN (AND (NULL SCREEN) (FBOUNDP 'X-SCREEN:START-X-SCREEN-VIA-CONSOLE-CHANNEL))
    (PROCESS-RUN-FUNCTION "Start X Screen via console channel"
      #'(LAMBDA ()
	  (PROCESS-WAIT "OK to start console"
	    #'(LAMBDA () (NULL *SYSTEM-INITIALIZATION-IN-PROGRESS*)))
	  (X-SCREEN:START-X-SCREEN-VIA-CONSOLE-CHANNEL))))
  (WHEN SCREEN
    (LET ((SCREENS (TV:CONSOLE-SCREENS-FOR-SELECTION (TV:SHEET-CONSOLE SCREEN)))
	  (MARGINS (IF SPECIAL-MARGIN-COMPONENTS
		       DW::*INITIAL-LISP-LISTENER-MARGIN-COMPONENTS*
		     DW::*LISP-LISTENER-MARGIN-COMPONENTS*)))
      (DOLIST (SCR SCREENS)
	(LET ((WINDOW (SEND SCR :GET 'TV:INITIAL-LISP-LISTENER)))
	  (WHEN (AND WINDOW
		     (MEMQ (SEND WINDOW :SUPERIOR) SCREENS)
		     (TV:WINDOW-IS-EXPOSABLE-INFERIOR WINDOW NIL))
	    ;; Found a suitable precreated window, make sure it is on the right screen
	    (UNLESS (EQ (SEND WINDOW :SUPERIOR) SCREEN)
	      (TV:SET-WINDOW-ON-USABLE-SCREEN WINDOW SCREEN NIL :FORCE-MOVE T))
	    ;; Make sure it has the right margin components
	    (UNLESS (= (LENGTH (SEND WINDOW :MARGIN-COMPONENTS)) (LENGTH MARGINS))
	      (SEND WINDOW :SET-MARGIN-COMPONENTS MARGINS))
	    ;; Return it
	    (RETURN-FROM FIND-OR-CREATE-INITIAL-LISP-LISTENER WINDOW))))
      ;; Couldn't find a suitable precreated window, so make a new one
      (LET ((WINDOW (STACK-LET ((MARGIN-OPTIONS (LIST :MARGIN-COMPONENTS MARGINS)))
		      (CL:APPLY #'TV:MAKE-WINDOW 'DW::DYNAMIC-LISP-LISTENER :SUPERIOR SCREEN
				MARGIN-OPTIONS))))
	(SETF (SEND SCREEN :GET 'TV:INITIAL-LISP-LISTENER) WINDOW)
	WINDOW))))

(DEFUN CREATE-SIMPLE-LISTENER-IF-NEEDED (LISTENER)
  (ignore listener)
  #+imach
  (UNLESS LISTENER
    (WHEN (SYSTEM-CASE
	    ((VLM)
	     (NOT (FBOUNDP 'X-SCREEN:START-X-SCREEN-VIA-CONSOLE-CHANNEL)))
	    ((UX)
	     (NOT (FBOUNDP 'X-SCREEN:START-X-SCREEN)))
	    ((MACIVORY)
	     (NOT (FBOUNDP 'RPC:CREATE-RPC-CONSOLE)))
	    (OTHERWISE NIL))
      (PROCESS:MAKE-PROCESS "Simple Lisp Listener"
			    :INITIAL-FUNCTION #'(LAMBDA (TERMINAL-IO)
						  (PROCESS-WAIT "Finish disk save"
						    #'(LAMBDA () (NULL SI:*DISK-SAVING*)))
						  (LISP-TOP-LEVEL1 TERMINAL-IO))
			    :INITIAL-FUNCTION-ARGUMENTS (LIST TERMINAL-IO)
			    :WARM-BOOT-ACTION 'PROCESS:PROCESS-WARM-BOOT-RESET
			    :RUN-REASONS NIL))))

(DEFUN INITIALIZE-LISP-LISTENER ()
  (LET* ((LISTENER (FIND-OR-CREATE-INITIAL-LISP-LISTENER TV:DEFAULT-SCREEN))
	 (TURN-SERVERS-ON (AND (NOT (EQUAL (SEND NET:*LOCAL-HOST* :SERVER-MACHINE) "YES"))
			       (NOT (EQ NET:*LOCAL-SITE* NETI:*DISTRIBUTION-SITE*))))
	 (SIMPLE-LISTENER (CREATE-SIMPLE-LISTENER-IF-NEEDED LISTENER)))
    ;; At this point if the window system is loaded, it is all ready to go
    ;; and the initial Lisp listener is about to be exposed and selected.  So do
    ;; any future typeout on it.  But if any typeout happened on the cold-load
    ;; stream, leave it there (clobbering the Lisp listener's bits).  This does not
    ;; normally happen, but just in case we do the set-cursorpos below so that
    ;; if anything strange gets typed out it won't get erased.  Note that normally
    ;; we do not do any typeout nor erasing on the cold-load-stream, to avoid bashing
    ;; the bits of whatever window was exposed before a warm boot.
    (COND (LISTENER
	    (SEND TERMINAL-IO :DESELECT)	;turn off blinker
	    (SEND LISTENER :EXPOSE)
	    (MULTIPLE-VALUE-BIND (LEFT TOP IGNORE IGNORE)
		(SEND LISTENER :VISIBLE-CURSORPOS-LIMITS)
	      (MULTIPLE-VALUE-BIND (IGNORE LISP-Y) (SEND LISTENER :READ-CURSORPOS)
		(MULTIPLE-VALUE-BIND (COLD-X COLD-Y) (SEND TERMINAL-IO :READ-CURSORPOS)
		  ;; The cold-load-stream coordinates are interpreted relative to the current
		  ;; viewport.
		  (WHEN (> (+ COLD-Y TOP) LISP-Y)
		    (SEND LISTENER :SET-CURSORPOS (+ COLD-X LEFT) (+ COLD-Y TOP))
		    ))))
	    (DO-INITIAL-DIALOGUE LISTENER TURN-SERVERS-ON))
	  (SIMPLE-LISTENER
	    (SEND TERMINAL-IO :SELECT)		;In case it's not already visible.
	    (DO-INITIAL-DIALOGUE TERMINAL-IO TURN-SERVERS-ON T))
	  (T
	    (SEND TERMINAL-IO :DESELECT)))	;turn off blinker
    ;; Now that everything is ready, set the global variable to indicate that
    ;; the window system is fully operational
    (SETQ TV:INITIAL-LISP-LISTENER LISTENER)
    (SETQ *SIMPLE-LISTENER-PROCESS* SIMPLE-LISTENER)
    ;; Now that -all- initialization has been completed, enable services if
    ;; appropriate.  Deciding whether it's appropriate was done above.
    (IF TURN-SERVERS-ON
	(ENABLE-SERVICES)
	#+IMach
	(SYSTEM-CASE
	  (Solstice
	    ;; Solstices need this service to get their consoles.  You
	    ;; might think this could be enabled solely for the
	    ;; *EMB-HOST*, if that were possible, but you would be
	    ;; wrong.  There's no rule that says Solstices can't be
	    ;; reached over the network.
	    (ENABLE-SERVICES '(:RPC)))
	  (Domino
	    ;; Domino needs this service to allow X terminals to ask for a console.  Further,
	    ;; for sites without X terminals or with X terminals that do not speak XDMCP,
	    ;; we'll enable TELNET and 3600-LOGIN service to allow access.  Even further, we
	    ;; enable UNIX-RSH and UNIX-REXEC so Unix machines can start an X screen via rsh
	    ;; and rexec.  Finally, for sites with UX software, we'll enable RPC service to
	    ;; allow access via the Genera program.  
	    (ENABLE-SERVICES '(:XDMCP :TELNET :3600-LOGIN :RPC :UNIX-RSH :UNIX-REXEC)))
	  (Otherwise NIL)))
    (COND (LISTENER
	    (SEND LISTENER :SELECT))
	  (SIMPLE-LISTENER
	    (PROCESS:ENABLE SIMPLE-LISTENER)))
    ;; The global value of TERMINAL-IO is a stream which goes to an auto-exposing
    ;; window.  Some processes, such as Lisp listeners, rebind it to something else.
    (SETQ TERMINAL-IO TV:DEFAULT-BACKGROUND-STREAM))
  (PROCESS-ALLOW-SCHEDULE)
  NIL)

(DEFUN DO-INITIAL-DIALOGUE (WINDOW TURN-SERVERS-ON &OPTIONAL (NO-WINDOW-SYSTEM-P NIL))
  (LET ((TV:MORE-PROCESSING-GLOBAL-ENABLE NIL))
    (CL:FRESH-LINE WINDOW)
    (COND (*DISK-SAVING*)
	  (*WARM-BOOTED-P*
	   ;; Put distance between this and any text typed out before warm boot.
	   (TERPRI WINDOW)
	   (SCT:PRINT-HERALD :STREAM WINDOW :BRIEF T))
	  ((OR NO-WINDOW-SYSTEM-P
	       (NOT (VARIABLE-BOUNDP #'DRAW-INITIAL-WINDOW))
	       (MULTIPLE-VALUE-BIND (WIDTH HEIGHT)
		   (SEND (SEND WINDOW :SCREEN) :INSIDE-SIZE)
		 (OR (< WIDTH 750.) (< HEIGHT 500.))))
	   (UNLESS *WARM-BOOTED-P*
	     (SEND-IF-HANDLES WINDOW :CLEAR-WINDOW)
	     (SEND-IF-HANDLES WINDOW :REFRESH))
	   (SEND-IF-HANDLES WINDOW :REFRESH-MARGINS)
	   (PRINT-HERALD :STREAM WINDOW)
	   (UNLESS (OR NO-WINDOW-SYSTEM-P *WARM-BOOTED-P*)
	     (LET* ((MODEL (MACHINE-MODEL))
		    (VERSIONS (SCT:COMPUTE-SYSTEM-VERSIONS NIL))
		    (TRADEMARK-HOLDERS (COMPUTE-TRADEMARK-HOLDERS VERSIONS MODEL NIL)))
	       (MULTIPLE-VALUE-BIND (IGNORE IGNORE IGNORE BOTTOM)
		   (MULTIPLE-VALUE-BIND (LEFT TOP)
		       (SEND WINDOW :READ-CURSORPOS)
		     (DRAW-LEGAL-FINE-PRINT WINDOW MODEL LEFT TOP NIL TRADEMARK-HOLDERS 5))
		 (SEND WINDOW :SET-CURSORPOS 0 BOTTOM))))
	   (TERPRI WINDOW))
	  (T
	   (DRAW-INITIAL-WINDOW WINDOW T)))
    ;; Turn servers on unless this machine is a "server" machine or we're system building.
    (WHEN (NOT TURN-SERVERS-ON)
      (FORMAT WINDOW "Note:  Servers are currently disabled.~2%"))
    (WHEN (AND *SITE-SYSTEM*
	       (NOT (SCT:FIND-SYSTEM-NAMED *SITE-SYSTEM* NIL T)))
      (FORMAT WINDOW "Note:  Local System ~A is not currently loaded.~2%" *SITE-SYSTEM*))
    (LET ((STANDARD-OUTPUT WINDOW))
      (SCT:PRINT-SYSTEM-STATUS-WARNING))
    #+IMach
    (UNLESS (CLI::FP-COPROCESSOR-ENABLED-P)
      (CL:CASE (SI:MACHINE-MODEL)
	((:XL400 :XL1200 :XL1250)		;These models are only configured with FPAs.
	 (FORMAT WINDOW "Warning:  Floating point coprocessor is missing, defective, or disabled.~2%"))))
    #+IMach
    (SYSTEM-CASE
      (XL1200
	(UNLESS (= (MERLIN-II-CACHE-CONTROL.MODE) %MERLIN-II-CACHE-MODE-ENABLED)
	  (FORMAT WINDOW "Warning:  On-board memory cache is defective or disabled.~2%")))
      (DOMINO
	(WHEN (= (%P-LDB CLI::%%DOMINO-CACHE-ENABLE CLI::%DOMINO-RESET-REGISTER) 1)
	  (FORMAT WINDOW "Warning:  On-board memory cache is defective or disabled.~2%")))
      (Otherwise NIL))
    (COND (*DISK-SAVING*)
	  (*WARM-BOOTED-P*
	   (IF (NULL WARM-BOOTED-PROCESS)
	       (FORMAT WINDOW "~%Warm booted.~%")	;We recovered safely.
	       (FORMAT WINDOW "~%Warm boot while running ~S.~@
			       Its variable bindings remain in effect; ~
			       its unwind-protects have been lost.~%"
		       WARM-BOOTED-PROCESS)))
	  (T
	   (FORMAT WINDOW "~&Please login.~%")
	   ;; Arrange not to **MORE** until after the end of the herald.  Even if you type
	   ;; ahead the login, it's a pretty good bet that you've seen the initial window.
	   (MULTIPLE-VALUE-BIND (NIL Y)
	       (SEND WINDOW :READ-CURSORPOS)
	     (SEND-IF-HANDLES WINDOW :SET-LAST-TYPEIN-Y Y))))))

(DEFUN LISP-TOP-LEVEL1 (&OPTIONAL (STREAM TERMINAL-IO))
  (LISP-COMMAND-LOOP STREAM :NAME "Lisp Top Level"))

;; This is an easy to call version of the command loop.  It works well with no arguments.  It
;; also works well to use it with TV:PROCESS-MIXIN and have the :BEFORE :INIT method of your
;; flavor do (SETQ TV:PROCESS '(LISP-COMMAND-LOOP)).
(DEFUN LISP-COMMAND-LOOP (&OPTIONAL (TERMINAL-IO TERMINAL-IO)
			  &KEY (NAME "Lisp Command Loop")
			  ABORT-FUNCTION
			  READ-FUNCTION
			  EVAL-FUNCTION
			  PRINT-FUNCTION
			  INITIAL-FORM
			  (STANDARD-VALUE-BINDINGS
			    (IF (AND (NOT (SYMBOLP TERMINAL-IO))
				     (STANDARD-VALUE-ENVIRONMENT-P TERMINAL-IO))
				(STANDARD-VALUE-ENVIRONMENT-BINDINGS TERMINAL-IO)
				NIL)))
  (WITH-COMMAND-LOOP-STREAM-BINDINGS
    (LISP-COMMAND-LOOP-INTERNAL NAME ABORT-FUNCTION
				READ-FUNCTION EVAL-FUNCTION PRINT-FUNCTION INITIAL-FORM
				STANDARD-VALUE-BINDINGS)))

(DEFMACRO WITH-STANDARD-VALUE-BINDINGS (BINDING-LIST &BODY BODY &ENVIRONMENT ENV)
  (DECLARE (ZWEI:INDENTATION 0 3 1 2))
  (ONCE-ONLY (BINDING-LIST &ENVIRONMENT ENV)
    `(MULTIPLE-VALUE-BIND (VARS VALS)
	 ;; The top cons is to make PUSH and DELETE work right.
	 (LOOP FOR (VAR . VAL) IN (CDR ,BINDING-LIST)
	       COLLECT VAR INTO VARS
	       COLLECT VAL INTO VALS		
	       FINALLY (RETURN (VALUES VARS VALS)))
       (STANDARD-VALUE-PROGV VARS VALS
	 ,@BODY))))

;;; This is a separate function so that Joshua can patch it.
(DEFUN SET-VALUE-FOR-* (NEW-*)
  (SETF *** **)
  (SETF ** *)
  (SETF * NEW-*))

;; This is the real command loop.
;; Note that the order of everything in this loop is important.
;; Note that STANDARD-VALUE-BINDINGS must be of the form `(nil . name-value-alist)
(DEFUN LISP-COMMAND-LOOP-INTERNAL (NAME &OPTIONAL ABORT-FUNCTION
				   READ-FUNCTION EVAL-FUNCTION PRINT-FUNCTION INITIAL-FORM
				   STANDARD-VALUE-BINDINGS)
  (DECLARE (SYS:DOWNWARD-FUNARG ABORT-FUNCTION READ-FUNCTION EVAL-FUNCTION PRINT-FUNCTION))
  (WITH-PROCESS-INTERACTIVE-PRIORITY ()
    (WITH-COMMAND-LOOP-VARIABLE-BINDINGS
      (WITH-NOTIFICATION-MODE (:BLAST)
	(DO (ABORTED-FLAG READ-FLAG READ-TYPE) (NIL)
	  (WITH-STANDARD-VALUE-BINDINGS STANDARD-VALUE-BINDINGS
	     (CATCH-ERROR-RESTART ((ERROR ABORT) "Return to ~A in ~A" NAME TERMINAL-IO)
	       (WHEN ABORTED-FLAG
		 (SEND STANDARD-OUTPUT ':FRESH-LINE)
		 (FUNCALL (OR ABORT-FUNCTION *COMMAND-LOOP-ABORT-FUNCTION*) NAME)
		 (SEND STANDARD-OUTPUT ':FRESH-LINE)
		 (SEND STANDARD-OUTPUT ':TYO #\NEWLINE))
	       (SETQ ABORTED-FLAG T)
	       (SEND STANDARD-OUTPUT ':FRESH-LINE)
	       (WHEN READ-FLAG
		 (SETQ +++ ++
		       ++ +
		       + -
		       READ-FLAG NIL))
	       (MULTIPLE-VALUE (- READ-TYPE)
		 (IF INITIAL-FORM
		     (VALUES (PROG1 INITIAL-FORM (SETQ INITIAL-FORM NIL)) NIL)
		   (FUNCALL (OR READ-FUNCTION *COMMAND-LOOP-READ-FUNCTION*))))
	       (LET ((SPECIAL
		       (CATCH 'COMMAND-LOOP-NEW-FORM
			 (COERCE-TO-LIST READ-TYPE
			   (IF (MEMQ ':FRESH-LINE READ-TYPE)
			       (SEND STANDARD-OUTPUT ':FRESH-LINE))
			   (SETQ READ-FLAG (NOT (MEMQ ':NO-READ READ-TYPE)))
			   (WHEN (AND (NOT (MEMQ ':NO-EVAL READ-TYPE))
				      (SETQ //
					    (PROG1 (MULTIPLE-VALUE-LIST
						     (WITH-PROCESS-NON-INTERACTIVE-PRIORITY ()
						       (FUNCALL
							 (OR EVAL-FUNCTION
							     *COMMAND-LOOP-EVAL-FUNCTION*)
							 -)))
						   (SETQ ////// ////
							 //// //))))
			     (SET-VALUE-FOR-* (CAR //))
			     (WITH-PROCESS-NON-INTERACTIVE-PRIORITY ()
			       (FUNCALL (OR PRINT-FUNCTION *COMMAND-LOOP-PRINT-FUNCTION*)
					//))))
			 NIL)))
		 (WHEN SPECIAL
		   (SETQ INITIAL-FORM SPECIAL)))
	       (SETQ ABORTED-FLAG NIL))))))))

;; BREAK

(DEFVAR OLD-STANDARD-INPUT)
(DEFVAR OLD-QUERY-IO)

;; This variable is a list of variables and forms producing the values to bind them
;; to.  They get bound by a BREAK.  It is not unreasonable for users to push onto
;; this list.  The bindings happen sequentially.  KBD-INTERCEPTED-CHARACTERS must
;; be bound back to its usual value since the editor and the input editor bind it
;; to NIL.  RUBOUT-HANDLER and the IE variables are bound to NIL so that a new level
;; of catch will be established.  These bindings are done inside of BREAK rather than
;; in *BREAK-BINDINGS* since :SAVE-INPUT-BUFFER and :RESTORE-INPUT-BUFFER must look
(DEFVAR *BREAK-BINDINGS*
	'((OLD-STANDARD-INPUT STANDARD-INPUT)
	  (OLD-QUERY-IO QUERY-IO)
	  (STANDARD-INPUT SYN-TERMINAL-IO)
	  (STANDARD-OUTPUT SYN-TERMINAL-IO)
	  (ERROR-OUTPUT SYN-TERMINAL-IO)
	  (DEBUG-IO SYN-TERMINAL-IO)
	  (QUERY-IO SYN-TERMINAL-IO)
	  (DW:*PRESENTATION-INPUT-CONTEXT* NIL)
	  (READ-PRESERVE-DELIMITERS NIL)
	  (*READ-SUPPRESS* NIL)
	  (XR-MAINTAIN-CORRESPONDENCE NIL)
	  (XR-CORRESPONDENCES NIL)
	  (KBD-INTERCEPTED-CHARACTERS KBD-STANDARD-INTERCEPTED-CHARACTERS)
	  (LOCAL-DECLARATIONS NIL)
	  (DEFAULT-CONS-AREA WORKING-STORAGE-AREA)
	  (DBG:*BOUND-HANDLERS* NIL)			;"Condition Wall"
	  (DBG:*DEFAULT-HANDLERS* NIL)))

(DEFUN BREAK-INTERNAL (TAG &OPTIONAL (CONDITIONAL T) ENV)
  (WHEN CONDITIONAL
    (WITH-PROCESS-INTERACTIVE-PRIORITY ()
      (LET ((*REMEMBERED-BINDING-WARNINGS* NIL))
	;; No need to warn when entering a new command loop, it's just stupid
	(WITH-INTERACTIVE-VALUES-BOUND (*BREAK-BINDINGS* NIL VARS VALS)
	  (UNWIND-PROTECT
	      (PROGN
		;; Deal with keyboard multiplexing in a way similar to the error-handler.
		;; If this is not the scheduler process, make sure it has a run reason
		;; in case we broke in the middle of code manipulating process data.
		;; If INHIBIT-SCHEDULING-FLAG is set, turn it off and print a warning.
		(WHEN (AND (NOT (NULL CURRENT-PROCESS))
			   (NULL (SEND CURRENT-PROCESS ':RUN-REASONS)))
		  (SEND CURRENT-PROCESS ':RUN-REASON 'BREAK))
		(WHEN (AND INHIBIT-SCHEDULING-FLAG
			   (NEQ TERMINAL-IO COLD-LOAD-STREAM))	;This will work wo-i.
		  (FORMAT T
			  "~&---> Turning off INHIBIT-SCHEDULING-FLAG, you might lose. <---")
		  (SETQ INHIBIT-SCHEDULING-FLAG NIL))
		;; Some people are rightly confused.
		(WHEN (AND (LISTP TAG) (EQ (CAR TAG) 'QUOTE))
		  (SETQ TAG (CADR TAG)))
		(SETQ TAG (FORMAT NIL "Breakpoint ~A" TAG))
		(FLET ((BREAK-INTERNAL1 ()
			 (BREAK-HERALD TAG T)
			 (TAGBODY DONT-ABORT
			   (LET ((VALUES
				   (CATCH 'BREAK-LOOP
				     (LET ((RUBOUT-HANDLER NIL)
					   (*INPUT-EDITOR-OPTIONS* NIL)
					   (*INPUT-EDITOR-TYPEIN* NIL))
				       (LISP-COMMAND-LOOP-INTERNAL
					 TAG 'BREAK-HERALD
					 #'(LAMBDA () (BREAK-READ-FUNCTION ENV))
					 #'(LAMBDA (FORM) (FUNCALL
							    *COMMAND-LOOP-BREAK-EVAL-FUNCTION*
							    FORM ENV))
					 *COMMAND-LOOP-BREAK-PRINT-FUNCTION*)))))
			     ;; Process special commands which were thrown to the BREAK-LOOP
			     ;; tag.  BREAK-READ-FUNCTION uses this to abort out of break
			     ;; loops and to implement the break (RETURN <foo>) "command".
			     (UNLESS (LISTP VALUES) (SETQ VALUES (LIST VALUES)))
			     (SELECTQ (CAR VALUES)
			       (:ABORT (PROCESS-ABORT CURRENT-PROCESS) (GO DONT-ABORT))
			       (:RETURN (SETQ VALUES (CDR VALUES))))
			     (VALUES-LIST VALUES)))))
		  (IF (OPERATION-HANDLED-P TERMINAL-IO :WITH-SAVED-STATE-FOR-BREAK)
		      (SEND TERMINAL-IO :WITH-SAVED-STATE-FOR-BREAK #'BREAK-INTERNAL1)
		      (BREAK-INTERNAL1))))))))))

(DEFUN BREAK-HERALD (NAME &OPTIONAL ENTER-P)
  (LET ((WIDTH (SEND-IF-HANDLES TERMINAL-IO :SIZE-IN-CHARACTERS)))
    (FORMAT T "~:[Back to ~;~&>~]~A.~:[  ~;~%~]~
	       Press ’ to continue or ‘ to quit.~%~@[~%~]"
	    ENTER-P NAME (AND WIDTH (< WIDTH 65.)) ENTER-P)))

(DEFVAR *BREAK-HELP-MESSAGE*
  "You are typing to a BREAK read-eval-print loop in ~A.~@
   Press ’ to return from this loop or ‘ to abort back to the previous command level.~@
   Type (RETURN <value>) to return a value from the BREAK function.~2%")

(DEFVAR *BREAK-SPECIAL-CHARACTERS* '(#\RESUME #\ABORT #\c-Z #\SUSPEND #\m-SUSPEND))

;; *COMMAND-LOOP-BREAK-READ-FUNCTION* must be distinct from *COMMAND-LOOP-READ-FUNCTION*
;; since READ-OR-CHARACTER invokes the input editor instead of READ-FORM.

(DEFUN COMMAND-LOOP-BREAK-READ-FUNCTION ()
  (WITH-INPUT-EDITING-OPTIONS ((:PARTIAL-HELP *BREAK-HELP-MESSAGE* TERMINAL-IO))
    (READ-OR-CHARACTER *BREAK-SPECIAL-CHARACTERS* STANDARD-INPUT #'READ-FORM)))

;; Read the first non-whitespace character specially, even if it is a key that would normally
;; be intercepted synchronously.  Once some text has been typed, however, interpret the
;; standard intercepted characters in the usual fashion.  Treat m-Suspend specially so that if
;; Suspend or c-Suspend is typed by accident, m-Suspend can then be typed without having too
;; many useless stack frames in the debugger.  Treat c-Z like Abort for convenience and for
;; compatibility with the debugger.

(DEFUN BREAK-READ-FUNCTION (&OPTIONAL ENV)
  (LOOP DO
    (MULTIPLE-VALUE-BIND (FORM FLAG CHAR)
	(LET ((*READ-FORM-ENVIRONMENT* ENV))
	  (FUNCALL *COMMAND-LOOP-BREAK-READ-FUNCTION*))
      ;; Flag can be :CHARACTER, :NO-READ, :NO-EVAL, or something returned by the input editor
      (COND ((EQ FLAG :CHARACTER)
	     (IF (CHAR= CHAR #\c-Z) (SETQ CHAR #\ABORT))
	     (COND ((MEM #'CHAR= CHAR KBD-STANDARD-INTERCEPTED-CHARACTERS)
		    (CONDITION-CASE ()
			(KBD-INTERCEPT-CHARACTER CHAR)
		      (ABORT (THROW 'BREAK-LOOP ':ABORT)))
		    (SEND STANDARD-OUTPUT :FRESH-LINE)
		    (SEND STANDARD-OUTPUT :TYO #\NEWLINE))
		   ((CHAR= CHAR #\RESUME)
		    (WHEN (OPERATION-HANDLED-P STANDARD-OUTPUT :BUFFER-X)
		      (SEND STANDARD-OUTPUT :SET-CURSORPOS
			    (SEND STANDARD-OUTPUT :BUFFER-X)
			    (SEND STANDARD-OUTPUT :BUFFER-Y)))
		    (SEND STANDARD-OUTPUT :STRING-OUT "[Resume]")
		    (THROW 'BREAK-LOOP '(:RETURN)))))
	    ((AND (LISTP FORM) (EQ (CAR FORM) 'RETURN))
	     (THROW 'BREAK-LOOP
	       `(:RETURN . ,(MULTIPLE-VALUE-LIST (EVAL (CADR FORM) ENV)))))
	    (T (RETURN (VALUES FORM FLAG)))))))
