;; -*- Mode: Lisp; Package: TV; Base: 8 -*-
;;;>
;;;> *****************************************************************************************
;;;> ** (c) Copyright 1998-1982 Symbolics, Inc.  All rights reserved.
;;;> ** Portions of font library Copyright (c) 1984 Bitstream, Inc.  All Rights Reserved.
;;;>
;;;>    The software, data, and information contained herein are proprietary to,
;;;> and comprise valuable trade secrets of, Symbolics, Inc., which intends 
;;;> to keep such software, data, and information confidential and to preserve them
;;;> as trade secrets.  They are given in confidence by Symbolics pursuant 
;;;> to a written license agreement, and may be used, copied, transmitted, and stored
;;;> only in accordance with the terms of such license.
;;;> 
;;;> Symbolics, Symbolics 3600, Symbolics 3675, Symbolics 3630, Symbolics 3640,
;;;> Symbolics 3645, Symbolics 3650, Symbolics 3653, Symbolics 3620, Symbolics 3610,
;;;> Zetalisp, Open Genera, Virtual Lisp Machine, VLM, Wheels, Dynamic Windows,
;;;> SmartStore, Semanticue, Frame-Up, Firewall, Document Examiner,
;;;> Delivery Document Examiner, "Your Next Step in Computing", Ivory, MacIvory,
;;;> MacIvory model 1, MacIvory model 2, MacIvory model 3, XL400, XL1200, XL1201,
;;;> Symbolics UX400S, Symbolics UX1200S, NXP1000, Symbolics C, Symbolics Pascal,
;;;> Symbolics Prolog, Symbolics Fortran, CLOE, CLOE Application Generator,
;;;> CLOE Developer, CLOE Runtime, Common Lisp Developer, Symbolics Concordia,
;;;> Joshua, Statice, and Minima are trademarks of Symbolics, Inc.
;;;> 
;;;> Symbolics 3670, Symbolics Common Lisp, Symbolics-Lisp, and Genera are registered
;;;> trademarks of Symbolics, Inc.
;;;>
;;;> GOVERNMENT PURPOSE RIGHTS LEGEND
;;;> 
;;;>      Contract No.: various
;;;>      Contractor Name: Symbolics, Inc.
;;;>      Contractor Address: c/o Ropes & Gray
;;;> 			 One International Place
;;;> 			 Boston, Massachusetts 02110-2624
;;;>      Expiration Date: 2/27/2018
;;;>      
;;;> The Government's rights to use, modify, reproduce, release, perform, display or
;;;> disclose this software are restricted by paragraph (b)(2) of the "Rights in
;;;> Noncommercial Computer Software and Noncommercial Computer Software Documentation"
;;;> contained in the above identified contracts.  No restrictions apply after the
;;;> expiration date shown above.  Any reproduction of the software or portions thereof
;;;> marked with this legend must also reproduce the markings.  Questions regarding
;;;> the Government's rights may be referred to the AS&T Contracts Office of the
;;;> National Reconnaissance Office, Chantilly, Virginia 20151-1715.
;;;> 
;;;>      Symbolics, Inc.
;;;>      c/o Ropes & Gray
;;;>      One International Place
;;;>      Boston, Massachusetts 02110-2624
;;;>      781-937-7655
;;;>
;;;> *****************************************************************************************
;;;>

;; Notifications
;; Rewritten January 1984 (just before the building of system 244) by Hornig and Hoffman.

(DEFSTRUCT (NOTIFICATION :CONC-NAME :LIST)
  TIME					   ;Universal time
  STRING
  WINDOW-OF-INTEREST)

;;

(DEFCONST *ALLOW-POP-UP-NOTIFICATIONS* T
  "If this is T then asynchronous notifications not handled by the selected process will be displayed in a pop-up window.
If it is NIL they will be only be noted in the who-line.")

;; Give the user 3 seconds to notice the beep and stop typing.  Until then, all typein is
;; forwarded to the previously selected window.  If this value is NIL, then the "type space to
;; flush" message will never be offered.

(DEFCONST UNEXPECTED-SELECT-DELAY 180.)

;; Wait up to twenty seconds for TV:SELECTED-WINDOW to become non-NIL, or to take on some
;; specific value.  This is used by the notification process when waiting for a selected
;; window to give the notification to, and by pop-up notification, when waiting to see if the
;; window popped up successfully.

(DEFCONST *SELECTED-WINDOW-DELAY* 1200.)

;; A process gets 3 seconds to take the notification from the delivery process.

(DEFCONST *NOTIFICATION-DELIVER-TIMEOUT* 180.)

;; Pop-up notifications should not occur more frequently than every 5 seconds, since there may
;; be an infinite loop issuing notifications.

(DEFCONST *NOTIFICATION-POP-UP-DELAY* 600.)

;; If 15 minutes elapse, then pop down the notification window, so that the window beneath
;; will no longer be locked.  This can also be set to NIL, meaning never pop down.

(DEFCONST *NOTIFICATION-POP-DOWN-DELAY* 54000.)

;; If T, create pop-up notification windows on the main screen.
;; If NIL, create pop-up windows on the screen of the selected window.

(DEFCONST *NOTIFICATION-POP-UP-ON-MAIN-SCREEN* NIL)


;; Print only those aspects of the time, UT, that differ from the current time.  Always print
;; hours, minutes, and seconds.  Notifications used to call TIME:PRINT-BRIEF-UNIVERSAL-TIME,
;; but that did not give enough information when many notifications occurred within one
;; minute.

(DEFUN DISPLAY-NOTIFICATION-TIME (UT &OPTIONAL (STREAM STANDARD-OUTPUT) (VERBOSE NIL))
  (MULTIPLE-VALUE-BIND (SECONDS MINUTES HOURS DAY MONTH YEAR)
      (TIME:DECODE-UNIVERSAL-TIME UT)
    (MULTIPLE-VALUE-BIND (IGNORE IGNORE IGNORE REF-DAY REF-MONTH REF-YEAR)
	(TIME:GET-TIME)
      ;; If not same day, print month and day numerically.
      ;; Be very careful to work if time isn't initialized!
      (IF (OR VERBOSE
	      (NOT (EQL DAY REF-DAY))
	      (NOT (EQL MONTH REF-MONTH))
	      (NOT (EQL YEAR REF-YEAR)))
	  (FORMAT STREAM "~D//~D~:[//~2,'0D~] " MONTH DAY (EQL YEAR REF-YEAR) YEAR))
      ;; Always print hours, minutes, and seconds, even if the same as now.
      (FORMAT STREAM "~2,'0D:~2,'0D:~2,'0D" HOURS MINUTES SECONDS))))

;; Known styles are :STREAM, :WINDOW, :POP-UP, or NIL.
;; :STREAM is used when the notification is to be merged in with other output to this stream.
;; :WINDOW is used when the notification has an entire window to be displayed in, although
;;   sometimes things other than notifications may be displayed in that window.  It is
;;   assumed that the window has been cleared before the notification is displayed.  This is
;;   used when notifications are displayed in the editor typein window.
;; :POP-UP is used by the notification pop-up window.
;; NIL is used by the notification scroll window and PRINT-NOTIFICATIONS.

(DEFUN DISPLAY-NOTIFICATION (STREAM NOTE &OPTIONAL STYLE WINDOW-WIDTH)
  (LET (X Y)
    (WHEN (MEMQ STYLE '(:STREAM :WINDOW :POP-UP))
      (SEND STREAM :BEEP))
    (WHEN (MEMQ STYLE '(:STREAM :WINDOW))
      (SEND STREAM :FRESH-LINE)
      (SEND STREAM :TYO #\[))
    ;; Print verbosely when style is nil, since that usually means that we are
    ;; printing to someplace "permanent".  Kludge, kludge, kludge...
    (DISPLAY-NOTIFICATION-TIME (NOTIFICATION-TIME NOTE) STREAM (NULL STYLE))
    (SEND STREAM :TYO #\SPACE)
    (LET ((INDENT (OR (SEND-IF-HANDLES STREAM :READ-CURSORPOS :CHARACTER) 0)))
      ;; If the window is narrow, don't indent as much.  Assume notifications,
      ;; including the time, may be as wide as 100 characters.
      (WHEN (OR WINDOW-WIDTH
		(SETQ WINDOW-WIDTH (SEND-IF-HANDLES STREAM :SIZE-IN-CHARACTERS)))
	(SETQ INDENT (MIN INDENT (- WINDOW-WIDTH 100.))))
      (SI:WITH-INDENTATION (STREAM (MAX 1 INDENT))
	;; Not :STRING-OUT so that sensitivity is restored.
	(PRINC (NOTIFICATION-STRING NOTE) STREAM)))
    (WHEN (EQ STYLE :POP-UP)
      (SEND STREAM :FRESH-LINE)
      (MULTIPLE-VALUE (X Y) (SEND STREAM :READ-CURSORPOS)))
    (WHEN (NOTIFICATION-WINDOW-OF-INTEREST NOTE)
      (SELECTQ STYLE
	((:STREAM :WINDOW)
	 (IF (OR (NOT (SEND STREAM :INTERACTIVE)) (TYPEP STREAM 'TV:SHEET))
	     (FORMAT STREAM "~& Select ~A by typing ~:C-0-S."
		     (NOTIFICATION-WINDOW-OF-INTEREST NOTE) #\FUNCTION)))
	(:POP-UP
	 (FORMAT STREAM
		 "~%Select ~A by typing ~:C-0-S, or by clicking the mouse on this window.~%"
		 (NOTIFICATION-WINDOW-OF-INTEREST NOTE) #\FUNCTION))))
    (WHEN (MEMQ STYLE '(:STREAM :WINDOW))
      (SEND STREAM :TYO #\]))
    (WHEN (EQ STYLE :STREAM)
      (SEND STREAM :TYO #\NEWLINE))
    (IF (EQ STYLE :POP-UP) (VALUES X Y))))

;; This should be using a compute-motion stream rather than consing a string.

(DEFUN NOTIFICATION-HEIGHT (NOTE WINDOW &OPTIONAL STYLE)
  (LET ((STRING (WITH-OUTPUT-TO-STRING (S)
		  (DISPLAY-NOTIFICATION S NOTE STYLE (SEND WINDOW :SIZE-IN-CHARACTERS))))
	(HEIGHT))
    (MULTIPLE-VALUE (NIL HEIGHT)
      (SEND WINDOW :COMPUTE-MOTION STRING 0 NIL 0 0 T 0 100000. 100000.))
    (RETURN-ARRAY STRING)
    HEIGHT))

(DEFUN NOTIFICATION-FITS (NOTE WINDOW)
  (MULTIPLE-VALUE-BIND (NIL WINDOW-HEIGHT)
      (SEND WINDOW :SIZE)
    ( (NOTIFICATION-HEIGHT NOTE WINDOW) WINDOW-HEIGHT)))

(DEFUN NOTIFICATION-NEXT-NOTE (LAST-NOTE)
  (LOOP FOR NOTEL ON NOTIFICATION-HISTORY
	FOR (NOTE . NEXT) = NOTEL
	DO (IGNORE NOTE)
	WHEN (OR (EQ NEXT LAST-NOTE)
		 (NULL NEXT))
	RETURN NOTEL))


;; The notification delivery process

;; Give the process controlling the selected window a chance to display the notification
;; for itself, if it wants to.  Return T if it took the notification; NIL if it didn't.

(DEFUN DELIVER-NOTIFICATION (STREAM NOTE)
  (LET ((NOTIFICATION-CELL (SEND STREAM :NOTIFICATION-CELL))
	(NOTIFICATION-MODE (SEND STREAM :NOTIFICATION-MODE)))
    (COND
      ;; Always ignore, before giving synchronous handling a chance.
      ((EQ NOTIFICATION-MODE :ALWAYS-IGNORE)
       T)
      ;; Always give a pop-up window, even if waiting for input.
      ((EQ NOTIFICATION-MODE :ALWAYS-POP-UP)
       NIL)
      ;; Check that the stream observes the notification protocol
      ((NOT NOTIFICATION-CELL)
       NIL)
      ;; Try to store the notification.  If one is already there, then give up.
      ((NOT (STORE-CONDITIONAL NOTIFICATION-CELL NIL NOTE))
       NIL)
      ;; If the notification was successfully stored, give the process a chance to take it.
      ;; At the moment, this happens sufficiently rarely so that SEND-IF-HANDLES is
      ;; acceptable.
      ((LET ((PROC (SEND-IF-HANDLES STREAM :PROCESS)))
	 (WHEN PROC
	   (PROCESS:PROCESS-WAKEUP PROC))
	 (PROCESS-WAIT-WITH-TIMEOUT "Take Notification" *NOTIFICATION-DELIVER-TIMEOUT*
				  #'(LAMBDA (LOC) (NULL (CDR LOC))) NOTIFICATION-CELL))
       T)
      ;; If the process doesn't take it, try to take it back.
      ((NOT (STORE-CONDITIONAL NOTIFICATION-CELL NOTE NIL))
       (IF (EQ (CDR NOTIFICATION-CELL) NIL)
	   T			;; The process took the notification after all.
	   (FERROR "The notification protocol was violated.~@
	        ~S was found in the notification cell."
		   (CDR NOTIFICATION-CELL))))
      ;; The window wants the notification discarded.
      ((EQ NOTIFICATION-MODE :IGNORE)
       T)
      ;; The window wants the notification blasted.
      ((EQ NOTIFICATION-MODE :BLAST)
       (BLAST-NOTIFICATION STREAM NOTE))
      ((NOT (MEMQ NOTIFICATION-MODE '(NIL :POP-UP)))
       (FERROR "The notification protocol was violated.~@
	        ~S was found as the notification mode." NOTIFICATION-MODE)))))

;; Print the notification on the window without attempting to synchronize with any other
;; processes also using that window.  Only do this if the process explicitly asks for it.
;; Ignore errors while printing the notification since that might trigger another
;; notification.  This function is also used as the equivalent of the pop-up notification
;; window for remote terminals.  The :FORCE-OUTPUT is a kludge until remote terminals know to
;; do this themselves.

(DEFUN BLAST-NOTIFICATION (STREAM NOTE)
  (PROCESS-RUN-FUNCTION '(:NAME "Blast notification" :PRIORITY 5)
			#'(LAMBDA (STREAM NOTE)
			    (IGNORE-ERRORS
			      (LET ((TV:MORE-PROCESSING-GLOBAL-ENABLE NIL))
				(DISPLAY-NOTIFICATION STREAM NOTE :STREAM))
			      (SEND STREAM :FORCE-OUTPUT)))
			STREAM NOTE))

(DEFUN DELIVER-NOTIFICATION-TO-CONSOLES (CONSOLES NOTE NEXT-LAST-DELIVERED LAST-POP-UP-TIMES)
  (DECLARE (VALUES NEXT-LAST-DELIVERED LAST-POP-UP-TIMES))
  (LOOP FOR CONSOLE IN CONSOLES
	FOR LAST-POP-UP-TIME = (OR (CDR (ASSQ CONSOLE LAST-POP-UP-TIMES)) 0)
	DO
    (LOOP WITH SW DO
      (PROCESS-WAIT-WITH-TIMEOUT "Selected Window" *SELECTED-WINDOW-DELAY*
	#'(LAMBDA (CONSOLE)
	    (OR (NOT (NULL (CONSOLE-SELECTED-WINDOW CONSOLE)))
		(NULL (CONSOLE-SCREEN CONSOLE))
		(NOT (SHEET-EXPOSED-P (CONSOLE-SCREEN CONSOLE)))))
	CONSOLE)
      (SETQ SW (CONSOLE-SELECTED-WINDOW CONSOLE))
      (COND
	;; Check if we shouldn't deliver it at all
	((OR (NULL (CONSOLE-SCREEN CONSOLE))
	     (NOT (SHEET-EXPOSED-P (CONSOLE-SCREEN CONSOLE))))
	 (RETURN))
	;; Try giving it to the selected window.
	((AND SW (DELIVER-NOTIFICATION SW NOTE))
	 (RETURN))
	;; If a pop-up window was visible just recently, then wait a while,
	;; or until the selected window changes
	((NOT (AND *ALLOW-POP-UP-NOTIFICATIONS*
		   (TIME-ELAPSED-P *NOTIFICATION-POP-UP-DELAY* LAST-POP-UP-TIME)))
	 (PROCESS-WAIT "Pop Up Wait"
	   #'(LAMBDA (LAST-POP-UP-TIME SW CONSOLE)
	       (OR (AND *ALLOW-POP-UP-NOTIFICATIONS*
			(TIME-ELAPSED-P *NOTIFICATION-POP-UP-DELAY* LAST-POP-UP-TIME))
		   (NEQ SW (CONSOLE-SELECTED-WINDOW CONSOLE))))
	   LAST-POP-UP-TIME SW CONSOLE))
	;; Try giving it to a pop-up window.  
	((POP-UP-NOTIFICATION NOTE SW (CONSOLE-SCREEN CONSOLE))
	 ;; Note pop-up time
	 (SETF (CDR (OR (ASSQ CONSOLE LAST-POP-UP-TIMES)
			(CAR (PUSH (CONS CONSOLE 0) LAST-POP-UP-TIMES))))
	       (TIME))
	 (RETURN))
	;; Otherwise, the notification system is broken.  Record this fact,
	;; but don't try to display it, or any other recently received
	;; notifications.
	(T
	 (TV:NOTIFY NIL "The notification system was unable to deliver a notification.")
	 (SETQ NEXT-LAST-DELIVERED NOTIFICATION-HISTORY)
	 (RETURN)))))
  (VALUES NEXT-LAST-DELIVERED LAST-POP-UP-TIMES))

;; This process receives notifications from TV:NOTIFY, using NOTIFICATION-HISTORY as a FIFO
;; queue.  It communicates with other processes only by writing through a locative which is
;; obtained by sending the :NOTIFICATION-CELL message to the selected window.  No user code
;; should ever run in this process.  A central process is used instead of having TV:NOTIFY
;; use PROCESS-RUN-FUNCTION so that notifications are delivered in order.

(DEFUN NOTIFICATION-DELIVERY-TOP-LEVEL ()
  (FLET ((RECORD-DELIVERY (NOTE NEXT-LAST-DELIVERED)
	   D,#TD1PsT[Begin using 006 escapes](1 0 (NIL 0) (NIL :ITALIC NIL) "CPTFONTI");; Record that a notification has been delivered.
0	   (WITHOUT-INTERRUPTS
	     (SETF *LAST-NOTIFICATION-TIME* (TIME))
	     (SETF *NOTIFICATION-LAST-DELIVERED* NEXT-LAST-DELIVERED))
	   ;; If there are any remote terminals connected, display it there also.
	   (DOLIST (TERMINAL NETI:*REMOTE-TERMINALS*)
	     (OR (DELIVER-NOTIFICATION TERMINAL NOTE)
		 (BLAST-NOTIFICATION TERMINAL NOTE)))))
    (LOOP WITH LAST-POP-UP-TIMES
	  DO (LOOP UNTIL (OR (NULL LAST-POP-UP-TIMES)
			     (PROCESS:PROCESS-BLOCK-WITH-TIMEOUT
			       (// (FLOAT
				     (- *NOTIFICATION-POP-UP-DELAY*
					(TIME-DIFFERENCE
					  (TIME)
					  (LOOP FOR PAIR IN LAST-POP-UP-TIMES
						MINIMIZE (CDR PAIR)))))
				   60.0)
			       "Notification Wait With Pop Up Notifications"
			       #'(LAMBDA ()
				   (NEQ NOTIFICATION-HISTORY *NOTIFICATION-LAST-DELIVERED*))))
		   DO (LOOP FOR PAIR IN LAST-POP-UP-TIMES
			    WHEN (TIME:TIME-ELAPSED-P *NOTIFICATION-POP-UP-DELAY* (CDR PAIR))
			      DO (SETQ LAST-POP-UP-TIMES (DELETE PAIR LAST-POP-UP-TIMES))))
	     (PROCESS:PROCESS-BLOCK
	       "Notification Wait"
	       #'(LAMBDA ()
		   (NEQ NOTIFICATION-HISTORY *NOTIFICATION-LAST-DELIVERED*)))
	  FOR NEXT-LAST-DELIVERED = (NOTIFICATION-NEXT-NOTE *NOTIFICATION-LAST-DELIVERED*)
	  FOR NOTE = (FIRST NEXT-LAST-DELIVERED)
	  FOR WINDOW-OF-INTEREST = (NOTIFICATION-WINDOW-OF-INTEREST NOTE)
	  FOR CONSOLE-OF-INTEREST = (AND WINDOW-OF-INTEREST (SHEET-CONSOLE WINDOW-OF-INTEREST))
	  FOR CONSOLES = (IF CONSOLE-OF-INTEREST (LIST CONSOLE-OF-INTEREST) *CONSOLES*)
	  IF CONSOLES
	    DO (MULTIPLE-VALUE (NEXT-LAST-DELIVERED LAST-POP-UP-TIMES)
		 (DELIVER-NOTIFICATION-TO-CONSOLES
		   CONSOLES NOTE NEXT-LAST-DELIVERED LAST-POP-UP-TIMES))
	       (RECORD-DELIVERY NOTE NEXT-LAST-DELIVERED)
	  ELSE IF #+3600 T
		  #+IMACH (SYS:SYSTEM-CASE (EMBEDDED NIL) (DOMINO NIL) (NATIVE T))
	    DO (DISPLAY-NOTIFICATION COLD-LOAD-STREAM NOTE :STREAM)
	       (RECORD-DELIVERY NOTE NEXT-LAST-DELIVERED)
	  ELSE
	    DO (CL:SLEEP (// *NOTIFICATION-POP-UP-DELAY* 60.0)
			 :SLEEP-REASON "Notification Wait for Console"))))

;; Notification pop-up window

(DEFUN DISPLAY-FLUSH-MESSAGE (WINDOW-OF-INTEREST)
  (OR WINDOW-OF-INTEREST (SEND TERMINAL-IO :TYO #\NEWLINE))
  (SEND TERMINAL-IO :STRING-OUT "Press any character to get rid of this window: "))

(DEFCONST *NOTIFICATION-POP-UP-HELP*
  "This is the notification pop-up window.~@
   It displays recently received notifications, and will remain visible until any character is typed.~@
   Immediately after this window is exposed, it will pause ~D second~:P before accepting user input.~@
   If you wish to force the window to pop-down during this waiting period, press ‘.~@
   Characters which are typed during this waiting period will be forwarded to the previously selected window.~%")

(DEFUN POP-UP-NOTIFICATION-INTERNAL (NOTE SW SP TERMINAL-IO NAME)
  (LET ((NOTIFICATION-CELL (SEND TERMINAL-IO :NOTIFICATION-CELL))
	(OLD-IO-BUFFER (AND SP (SEND SP :IO-BUFFER)))
	(WINDOW-OF-INTEREST (NOTIFICATION-WINDOW-OF-INTEREST NOTE))
	(OLD-SELECTED-WINDOW SW)
	SELECT-TIME NOTIFICATION-TIME X1 Y1)
    (SEND TERMINAL-IO :SET-LABEL NAME)
    (UNWIND-PROTECT
      (CONDITION-CASE ()
	   (PROGN
	     ;; Turn off asynchronous characters while selecting the window, since it isn't
	     ;; safe to type c-Abort here.  This is a bug in the window system, of course.
	     (LET ((OLD-ASYNC (SEND TERMINAL-IO :ASYNCHRONOUS-CHARACTERS)))
	       (UNWIND-PROTECT
		   (PROGN (SEND TERMINAL-IO :SET-ASYNCHRONOUS-CHARACTERS NIL)
			  (SEND TERMINAL-IO :SELECT))
		 (SEND TERMINAL-IO :SET-ASYNCHRONOUS-CHARACTERS OLD-ASYNC)))
	     (MULTIPLE-VALUE (X1 Y1) (DISPLAY-NOTIFICATION TERMINAL-IO NOTE :POP-UP))
	     ;; Now that the window and the message are visible on the screen, it's time
	     ;; to start the various time delays clocking.
	     (SETQ SELECT-TIME (SETQ NOTIFICATION-TIME (TIME)))
	     (LOOP WITH CHAR
		   DOING
	       (SEND TERMINAL-IO :INPUT-WAIT
		     (IF SELECT-TIME "Unexpected Select")
		     #'(LAMBDA (WINDOW CELL SELECT-TIME NOTIFICATION-TIME)
			 (OR (CDR CELL)
			     (AND SELECT-TIME
				  UNEXPECTED-SELECT-DELAY
				  (TIME-ELAPSED-P UNEXPECTED-SELECT-DELAY SELECT-TIME))
			     (AND *NOTIFICATION-POP-DOWN-DELAY*
				  (TIME-ELAPSED-P *NOTIFICATION-POP-DOWN-DELAY*
						  NOTIFICATION-TIME))
			     (NEQ WINDOW (SHEET-SELECTED-WINDOW WINDOW))))
		     TERMINAL-IO NOTIFICATION-CELL SELECT-TIME NOTIFICATION-TIME)
	       (COND
		 ;; If the window is manually deselected, then deactivate it.
		 ((NEQ TERMINAL-IO (SHEET-SELECTED-WINDOW TERMINAL-IO))
		  (SEND TERMINAL-IO :DEACTIVATE)
		  (SETQ OLD-SELECTED-WINDOW NIL)
		  (RETURN NIL))
		 ;; If a new notification arrives, display it in the appropriate place,
		 ;; taking care to erase and replace the flush message.
		 ((SETQ NOTE (SEND TERMINAL-IO :RECEIVE-NOTIFICATION))
		  (SETQ WINDOW-OF-INTEREST (NOTIFICATION-WINDOW-OF-INTEREST NOTE))
		  (MULTIPLE-VALUE-BIND (X2 Y2)
		      (SEND TERMINAL-IO :READ-CURSORPOS)
		    (SEND TERMINAL-IO :CLEAR-BETWEEN-CURSORPOSES X1 Y1 X2 Y2)
		    (SEND TERMINAL-IO :SET-CURSORPOS X1 Y1))
		  (MULTIPLE-VALUE (X1 Y1) (DISPLAY-NOTIFICATION TERMINAL-IO NOTE :POP-UP))
		  (UNLESS SELECT-TIME
		    (DISPLAY-FLUSH-MESSAGE WINDOW-OF-INTEREST))
		  (SETQ NOTIFICATION-TIME (TIME)))
		 ;; After the user has had enough time to see the window,
		 ;; display the flush message.
		 ((AND SELECT-TIME
		       UNEXPECTED-SELECT-DELAY
		       (TIME-ELAPSED-P UNEXPECTED-SELECT-DELAY SELECT-TIME))
		  (SETQ SELECT-TIME NIL)
		  (DISPLAY-FLUSH-MESSAGE WINDOW-OF-INTEREST))
		 ;; Don't let the window remain on the screen forever.
		 ((AND *NOTIFICATION-POP-DOWN-DELAY*
		       (TIME-ELAPSED-P *NOTIFICATION-POP-DOWN-DELAY* NOTIFICATION-TIME))
		  (RETURN NIL))
		 ;; The user types a character.
		 ((SETQ CHAR (SEND TERMINAL-IO :ANY-TYI-NO-HANG))
		  (COND
		    ;; A blip of some kind, probably a mouse click.
		    ((LISTP CHAR)
		     (WHEN (AND WINDOW-OF-INTEREST
				(EQ (CAR CHAR) :MOUSE-BUTTON))
		       (DELAYING-SCREEN-MANAGEMENT
			 (SEND TERMINAL-IO :DEACTIVATE)
			 (SETQ OLD-SELECTED-WINDOW NIL)
			 (SEND WINDOW-OF-INTEREST :MOUSE-SELECT NIL))))
		    ;; If the user has seen the window, then pop down immediately,
		    ;; unless † is typed.
		    ((NOT SELECT-TIME)
		     (IF (CHAR CHAR #\HELP) (RETURN NIL))
		     (SEND TERMINAL-IO :CLEAR-WINDOW)
		     (FORMAT T *NOTIFICATION-POP-UP-HELP*
			     (AND UNEXPECTED-SELECT-DELAY (// UNEXPECTED-SELECT-DELAY 60.)))
		     (DISPLAY-FLUSH-MESSAGE NIL))
		    ;; Otherwise, forward typeahead to the old IO buffer.
		    ;; ‘ may be typed to force the window to pop down during this time.
		    (OLD-IO-BUFFER
		     (IO-BUFFER-PUT OLD-IO-BUFFER CHAR T)))))))
	 (OUTPUT-ON-DEEXPOSED-SHEET))
      (WHEN OLD-SELECTED-WINDOW
	;; More kludges as an excuse for not having an activity system.
	;; First, deexpose the notification window, do auto-exposure, and select something.
	(SEND TERMINAL-IO :DEACTIVATE)
	;; Now select the previous activity if it is exposed, but if selecting it
	;; would require exposing and deexposing windows, leave well enough alone.
	;; Be careful not to select an incorrect window within that activity, which
	;; could happen when using dynamic menus.  The only time we actually send a
	;; :SELECT is when the screen manager has multiple activities to choose from
	;; and it chooses the wrong one.
	(WHEN (AND (TYPEP OLD-SELECTED-WINDOW 'SHEET)
		   (LET ((SW (SHEET-SELECTED-WINDOW OLD-SELECTED-WINDOW)))
		     (NEQ (AND SW (SEND SW :ALIAS-FOR-SELECTED-WINDOWS)) OLD-SELECTED-WINDOW))
		   (NOT (ZEROP (SHEET-ALL-EXPOSED OLD-SELECTED-WINDOW))))
	  (SEND OLD-SELECTED-WINDOW :SELECT))))))

;; 400 is the height of the Converse pop-up window.  Keep them the same so that it looks
;; familiar to the user.

(DEFWINDOW-RESOURCE NOTIFICATION-POP-UP-WINDOW ()
  :MAKE-WINDOW (POP-UP-TEXT-WINDOW :NAME "Notification Pop Up Window"
				   :HEIGHT 400
				   :DEEXPOSED-TYPEOUT-ACTION :ERROR)
  :SUPERIOR DEFAULT-SCREEN
  :REUSABLE-WHEN :DEACTIVATED)

;; Return T if SELECTED-WINDOW became the pop-up window.
;; Return NIL if it timed out.

(DEFUN POP-UP-NOTIFICATION
       (NOTE SW &OPTIONAL (DEFAULT-NOTIFICATION-WINDOW (AND SW (CONSOLE-DEFAULT-SUPERIOR SW))))
  (LET ((NOTIFICATION-WINDOW (OR (FIND-NOTIFICATION-WINDOW SW) DEFAULT-NOTIFICATION-WINDOW)))
    (WHEN NOTIFICATION-WINDOW 
      (LET* ((WINDOW (ALLOCATE-RESOURCE 'NOTIFICATION-POP-UP-WINDOW NOTIFICATION-WINDOW))
	     (NAME (IF SW (FORMAT NIL "Notification: ~A" (OR (SEND-IF-HANDLES SW :NAME) SW))
		     (SEND WINDOW :NAME))))
	(PROCESS-RUN-FUNCTION `(:NAME ,NAME :PRIORITY 5)
	  #'POP-UP-NOTIFICATION-INTERNAL NOTE
	  ;; The thing to reselect when popping down is the activity, not the
	  ;; particular pane
	  (AND SW (SEND SW :ALIAS-FOR-SELECTED-WINDOWS)) SW WINDOW NAME)
	(PROCESS-WAIT-WITH-TIMEOUT "Pop Up Selection" *SELECTED-WINDOW-DELAY*
	  #'(LAMBDA (WINDOW)
	      (EQ WINDOW (SHEET-SELECTED-WINDOW WINDOW)))
	  WINDOW)))))

;;
;; This finds an appropriate window to put the notification near, taking into
;; account the window's parent screen's preference for notifications.
;;
;; This gives each window in the path to the screen the opportunity to
;; accept the notification, which allows smart frames (eg. live window frames)
;; to accept the notifications rather than the screen.  This also
;; gives smart screens (eg. color screens) the opportunity to refuse them.
;;
(DEFUN FIND-NOTIFICATION-WINDOW (&REST POSSIBILITIES)
  (BLOCK FIND-NOTIFICATION-WINDOW
    (DOLIST (W POSSIBILITIES)
      (LOOP FOR W = W THEN (SEND W :SUPERIOR) WHILE (INSTANCEP W) DO
	(WHEN (SEND-IF-HANDLES W :ACCEPT-POP-UP-NOTIFICATIONS)
	  (RETURN-FROM FIND-NOTIFICATION-WINDOW W))))))

;; The main notification routine.

(DEFUN NOTIFY (WINDOW-OF-INTEREST FORMAT-CONTROL &REST FORMAT-ARGS)
  "Notify the user with an unsolicited message.
The message is generated from FORMAT-CONTROL and FORMAT-ARGS.
If WINDOW-OF-INTEREST is non-NIL, it is a window to be made available to
Function-0-S and maybe another way depending on who prints the notification."
  (WHEN WINDOW-OF-INTEREST
    (SETQ WINDOW-OF-INTEREST (SI:FOLLOW-SYN-STREAM WINDOW-OF-INTEREST)))
  (CHECK-ARG WINDOW-OF-INTEREST
	     (OR (NULL WINDOW-OF-INTEREST)
		 (TYPEP WINDOW-OF-INTEREST 'SHEET))
	     "a window or NIL")
  (CHECK-ARG FORMAT-CONTROL
	     (OR (STRINGP FORMAT-CONTROL) (LISTP FORMAT-CONTROL))
	     "a FORMAT control string or list")
  ;; Save away the notification.
  (LET ((NOTE (MAKE-NOTIFICATION
		TIME (TIME:GET-UNIVERSAL-TIME)
		STRING (DW:WITH-OUTPUT-TO-PRESENTATION-RECORDING-STRING (STREAM)
			 (LEXPR-FUNCALL #'FORMAT STREAM FORMAT-CONTROL FORMAT-ARGS))
		WINDOW-OF-INTEREST WINDOW-OF-INTEREST)))
    (WITHOUT-INTERRUPTS
      (PUSH NOTE NOTIFICATION-HISTORY)
      (SETF *LAST-NOTIFICATION-TIME* (TIME))
      (multiple-value-prog1
	(WHEN WINDOW-OF-INTEREST			;Make this window "interesting"
	  (SI:PUSH* WINDOW-OF-INTEREST BACKGROUND-INTERESTING-WINDOWS)
	  ;; Try to make available to sys menu
	  (WHEN (SHEET-CAN-BE-ACTIVATED-FOR-DEBUGGER WINDOW-OF-INTEREST)
	    (SEND WINDOW-OF-INTEREST :ACTIVATE)))
	(process:process-wakeup *notification-process*)))))	;but don't bother if locked

;; This used to be more careful than NOTIFY.
(DEFUN CAREFUL-NOTIFY (WINDOW-OF-INTEREST IGNORE FORMAT-CONTROL &REST FORMAT-ARGS)
  (LEXPR-FUNCALL #'NOTIFY WINDOW-OF-INTEREST FORMAT-CONTROL FORMAT-ARGS))

(COMPILER:MAKE-OBSOLETE CAREFUL-NOTIFY "use TV:NOTIFY instead.")


;; Displaying notifications
;; Two "commands" are defined here.  One is teletype oriented; the other is window oriented.

;; Function that knows how to print a range of notifications.  Considers the
;; most recent notification to be number 0.  Default range is the whole list.
;; Don't try to handle requests outside the range.  Be sensible but don't overdo
;; it.  Negative low ends clipped to zero, excessive high ends clipped to end of
;; list, negative high ends are silly case but are treated by taking abs.

(DEFUN PRINT-NOTIFICATIONS (&OPTIONAL (FROM 0) (TO (1- (LENGTH NOTIFICATION-HISTORY))))
  (SETQ FROM (MAX FROM 0))			;ensure not negative
  (WHEN (< TO 0) (SETQ TO (ABS TO)))		;also not negative, but in different way
  (WHEN (> FROM TO) (SWAPF FROM TO))		;Save user if confused about
						;where beginning of list is
  (SETQ TO (MIN TO (1- (LENGTH NOTIFICATION-HISTORY))))	;Clip TO arg to max allowed
  (LET ((COUNT (IF (OR (> FROM TO) (> FROM (1- (LENGTH NOTIFICATION-HISTORY))))
		   0				;if from still >, whole range is outside
		   (1+ (- TO FROM))))		;how many to print
	(RANGE (NTHCDR FROM NOTIFICATION-HISTORY)))
    (FORMAT T "~&~:[No notifications.~;~:[No notifications in range.~;~D Notification~:P~:[~;, from ~D to ~D~]:~]~]~%"
	    NOTIFICATION-HISTORY RANGE COUNT (> COUNT 1) FROM TO)
    (LOOP FOR NOTE IN RANGE 
	  REPEAT COUNT DO
	  (SEND STANDARD-OUTPUT :FRESH-LINE)
	  (DISPLAY-NOTIFICATION STANDARD-OUTPUT NOTE))
    (WITHOUT-INTERRUPTS
      (WHEN (TAILP *NOTIFICATION-LAST-DELIVERED* RANGE)
	(SETF *LAST-NOTIFICATION-TIME* (TIME))
	(SETF *NOTIFICATION-LAST-DELIVERED* RANGE))))
  NIL)

;; MODE may take on the values :POP-UP, :BLAST, and :IGNORE.  :POP-UP is the default.
;; NIL is also accepted as a synonym for :POP-UP, for compatibility with Rel 5.
;; See also WITH-CURSOR-VISIBILITY and WITH-LABEL.

(DEFUN WITH-NOTIFICATION-MODE-INTERNAL (NEW-MODE STREAM CONTINUATION)
  (DECLARE (DOWNWARD-FUNARG CONTINUATION))
  (LET ((MODE (SEND-IF-HANDLES STREAM :NOTIFICATION-MODE)))
    (UNWIND-PROTECT
	(PROGN
	  (SEND-IF-HANDLES STREAM :SET-NOTIFICATION-MODE NEW-MODE)
	  (FUNCALL CONTINUATION))
      (SEND-IF-HANDLES STREAM :SET-NOTIFICATION-MODE MODE))))

(DEFMACRO WITH-NOTIFICATION-MODE ((NEW-MODE &OPTIONAL STREAM) &BODY BODY)
  `(WITH-NOTIFICATION-MODE-INTERNAL ,NEW-MODE ,(OR STREAM 'STANDARD-OUTPUT)
				    (LAMBDA () . ,BODY)))

;; Start up the mess

(DEFVAR *NOTIFICATION-PROCESS* NIL)

(DEFUN INITIALIZE-NOTIFY-SYSTEM ()
  (WHEN *NOTIFICATION-PROCESS*
    (PROCESS-KILL *NOTIFICATION-PROCESS*)
    (SETF *NOTIFICATION-PROCESS* NIL))
  (SETF *NOTIFICATION-PROCESS*
	(MAKE-PROCESS "Notification Delivery" :PRIORITY 5 :SYSTEM-PROCESS T))
  (PROCESS:ENABLE-PROCESS-FLAG *NOTIFICATION-PROCESS* :NO-KBD-ARREST)
  (PROCESS-PRESET *NOTIFICATION-PROCESS* 'NOTIFICATION-DELIVERY-TOP-LEVEL)
  (PROCESS-ENABLE *NOTIFICATION-PROCESS*))

;; Initializations

(ADD-INITIALIZATION "Forget old notifications"
		    '(SETQ NOTIFICATION-HISTORY NIL
			   *NOTIFICATION-LAST-DELIVERED* NIL)
		    '(:BEFORE-COLD))

(ADD-INITIALIZATION "Notify system"
		    '(INITIALIZE-NOTIFY-SYSTEM)
		    '(:ONCE))
