;; -*- Mode: Lisp; Package: Hacks; Base: 8 -*-

;; Pictures drawn with line graphics, plus associated window support.
;; Written by Carl Hoffman.

;;;>
;;;> *****************************************************************************************
;;;> ** (c) Copyright 1998-1982 Symbolics, Inc.  All rights reserved.
;;;> ** Portions of font library Copyright (c) 1984 Bitstream, Inc.  All Rights Reserved.
;;;>
;;;>    The software, data, and information contained herein are proprietary to,
;;;> and comprise valuable trade secrets of, Symbolics, Inc., which intends 
;;;> to keep such software, data, and information confidential and to preserve them
;;;> as trade secrets.  They are given in confidence by Symbolics pursuant 
;;;> to a written license agreement, and may be used, copied, transmitted, and stored
;;;> only in accordance with the terms of such license.
;;;> 
;;;> Symbolics, Symbolics 3600, Symbolics 3675, Symbolics 3630, Symbolics 3640,
;;;> Symbolics 3645, Symbolics 3650, Symbolics 3653, Symbolics 3620, Symbolics 3610,
;;;> Zetalisp, Open Genera, Virtual Lisp Machine, VLM, Wheels, Dynamic Windows,
;;;> SmartStore, Semanticue, Frame-Up, Firewall, Document Examiner,
;;;> Delivery Document Examiner, "Your Next Step in Computing", Ivory, MacIvory,
;;;> MacIvory model 1, MacIvory model 2, MacIvory model 3, XL400, XL1200, XL1201,
;;;> Symbolics UX400S, Symbolics UX1200S, NXP1000, Symbolics C, Symbolics Pascal,
;;;> Symbolics Prolog, Symbolics Fortran, CLOE, CLOE Application Generator,
;;;> CLOE Developer, CLOE Runtime, Common Lisp Developer, Symbolics Concordia,
;;;> Joshua, Statice, and Minima are trademarks of Symbolics, Inc.
;;;> 
;;;> Symbolics 3670, Symbolics Common Lisp, Symbolics-Lisp, and Genera are registered
;;;> trademarks of Symbolics, Inc.
;;;>
;;;> GOVERNMENT PURPOSE RIGHTS LEGEND
;;;> 
;;;>      Contract No.: various
;;;>      Contractor Name: Symbolics, Inc.
;;;>      Contractor Address: c/o Ropes & Gray
;;;> 			 One International Place
;;;> 			 Boston, Massachusetts 02110-2624
;;;>      Expiration Date: 2/27/2018
;;;>      
;;;> The Government's rights to use, modify, reproduce, release, perform, display or
;;;> disclose this software are restricted by paragraph (b)(2) of the "Rights in
;;;> Noncommercial Computer Software and Noncommercial Computer Software Documentation"
;;;> contained in the above identified contracts.  No restrictions apply after the
;;;> expiration date shown above.  Any reproduction of the software or portions thereof
;;;> marked with this legend must also reproduce the markings.  Questions regarding
;;;> the Government's rights may be referred to the AS&T Contracts Office of the
;;;> National Reconnaissance Office, Chantilly, Virginia 20151-1715.
;;;> 
;;;>      Symbolics, Inc.
;;;>      c/o Ropes & Gray
;;;>      One International Place
;;;>      Boston, Massachusetts 02110-2624
;;;>      781-937-7655
;;;>
;;;> *****************************************************************************************
;;;>
(DEFMACRO DRAW-LINE (X0 Y0 X1 Y1 &REST REST)
  `(SEND STANDARD-OUTPUT :DRAW-LINE ,X0 ,Y0 ,X1 ,Y1 . ,REST))

(DEFMACRO DRAW-LINE-RELATIVE (X Y DX DY &REST REST)
  `(SEND STANDARD-OUTPUT :DRAW-LINE ,X ,Y (+ ,X ,DX) (+ ,Y ,DY) . ,REST))

(DEFMACRO DRAW-LINES (ALU &REST POINTS)
  `(SEND STANDARD-OUTPUT :DRAW-LINES ,ALU . ,POINTS))

;; Useful constants

(DEFMACRO DEFINE-CONSTANT (VAR VAL)
  `(DEFCONST ,VAR #+CADR (SMALL-FLOAT ,VAL) #-CADR ,VAL))

(DEFINE-CONSTANT  	(ATAN 0 -1))
(DEFINE-CONSTANT //2	(ATAN 1 0))
(DEFINE-CONSTANT 2 	(* 2 ))
(DEFINE-CONSTANT SQRT-2 (SQRT 2))

;; Point vector resource.  2 by N long arrays.  Later, make the desired vector size a
;; parameter and be selective about which vector is returned.  The number of points
;; actually in use is stored in the zeroth element of the array leader.

;; This should be converted to use the data stack.

(DEFRESOURCE POINT-VECTOR ()
  :CONSTRUCTOR (MAKE-ARRAY '(2 1_7) :LEADER-LENGTH 1))

;; ((VARIABLE &OPTIONAL FILL-POINTER) &BODY BODY)

(DEFMACRO USING-POINT-VECTOR (SPECIFIER &BODY BODY)
  (LET ((VARIABLE (CAR SPECIFIER))
	(FILL-POINTER (IF (CDR SPECIFIER) (CADR SPECIFIER))))
    `(USING-RESOURCE (,VARIABLE POINT-VECTOR)
       ,@ (IF FILL-POINTER `((SETF (FILL-POINTER ,VARIABLE) ,FILL-POINTER)))
       ,@ BODY)))

(DEFMACRO XREF (VECTOR POINT) `(AREF ,VECTOR 0 ,POINT))
(DEFMACRO YREF (VECTOR POINT) `(AREF ,VECTOR 1 ,POINT))
(DEFMACRO XSET (VALUE VECTOR POINT) `(ASET ,VALUE ,VECTOR 0 ,POINT))
(DEFMACRO YSET (VALUE VECTOR POINT) `(ASET ,VALUE ,VECTOR 1 ,POINT))

;; Rotate a vector by one point, and store into a second.

(DEFUN POINT-VECTOR-ROTATE (TARGET-VECTOR SOURCE-VECTOR)
  (LOOP WITH FILL-POINTER = (FILL-POINTER TARGET-VECTOR)
	FOR POINT FROM 0 BELOW FILL-POINTER
	FOR POINT+1 FROM 1
	DO
    (IF (= POINT+1 FILL-POINTER) (SETQ POINT+1 0))
    (XSET (XREF SOURCE-VECTOR POINT) TARGET-VECTOR POINT+1)
    (YSET (YREF SOURCE-VECTOR POINT) TARGET-VECTOR POINT+1)))

;; Subtract one vector from another, and store into a third.

(DEFUN POINT-VECTOR-DIFFERENCE (TARGET-VECTOR A-VECTOR B-VECTOR)
  (LOOP FOR I FROM 0 BELOW (FILL-POINTER TARGET-VECTOR)
	DO
    (XSET (- (XREF A-VECTOR I) (XREF B-VECTOR I)) TARGET-VECTOR I)
    (YSET (- (YREF A-VECTOR I) (YREF B-VECTOR I)) TARGET-VECTOR I)))

;; Initialize a vector to a set of random points in two-space, within the planar
;; segment specified.

(DEFUN POINT-VECTOR-RANDOMIZE (VECTOR WIDTH HEIGHT)
  (LOOP FOR I FROM 0 BELOW (FILL-POINTER VECTOR)
	DO
    (XSET (RANDOM WIDTH) VECTOR I)
    (YSET (RANDOM HEIGHT) VECTOR I)))

;; Information needed by the dynamic picture drawers.

(DEFUN WINDOW-INFO-DYNAMIC ()
  (MULTIPLE-VALUE-BIND (WIDTH HEIGHT)
      (SEND STANDARD-OUTPUT :INSIDE-SIZE)
    (VALUES WIDTH HEIGHT (* WIDTH HEIGHT))))

;; Information needed by the static picture drawers.

(DEFUN WINDOW-INFO-STATIC (EXTENT-FRACTION EQUALIZE? &OPTIONAL (OFFSET 0))
  (MULTIPLE-VALUE-BIND (WIDTH HEIGHT)
      (SEND STANDARD-OUTPUT :INSIDE-SIZE)
    (LET* ((X-CENTER (// WIDTH 2))
	   (Y-CENTER (// HEIGHT 2))
	   (X-EXTENT (- (FIX (* X-CENTER EXTENT-FRACTION)) OFFSET))
	   (Y-EXTENT (- (FIX (* Y-CENTER EXTENT-FRACTION)) OFFSET)))
      (IF EQUALIZE?
	  (SETQ X-EXTENT (MIN X-EXTENT Y-EXTENT) Y-EXTENT X-EXTENT))
      (VALUES X-CENTER Y-CENTER X-EXTENT Y-EXTENT))))

;; X: 0  .  Y: 0  .
;; Multiply coordinates by K to map to window coordinates.
;; K = D/SQRT-2 where D is the window distance from 0,0 to 1,1.  K^2 = D^2/2.
;; Y = 1/X.  WY = K / (WX / K) = K^2 / WX.
;; Then shift X and Y to terminate at axis endpoints.

(DEFUN HYPERBOLA (&OPTIONAL (EXTENT-FRACTION 0.9) (EQUALIZE? NIL)
		  (HYPERBOLA-FRACTION 0.5) (DWX 3) (OFFSET 1))
  (MULTIPLE-VALUE-BIND (X-CENTER Y-CENTER X-EXTENT Y-EXTENT)
      (WINDOW-INFO-STATIC EXTENT-FRACTION EQUALIZE? OFFSET)
    (LET* ((D (FIX (* (MIN X-EXTENT Y-EXTENT) HYPERBOLA-FRACTION)))
	   (K (// D SQRT-2))
	   (DX (// DWX K))
	   (X-MAX (// X-EXTENT K))
	   (Y-MAX (// Y-EXTENT K))
	   (X-MIN (// 1 Y-MAX))
	   (Y-MIN (// 1 X-MAX))
	   ;; Since we will shift by X-MIN and Y-MIN, scale up linearly so
	   ;; that we precisely fit into X-EXTENT and Y-EXTENT.
	   (KX (* K (// (+ X-MIN X-MAX) X-MAX)))
	   (KY (* K (// (+ Y-MIN Y-MAX) Y-MAX))))
      (LOOP FOR SX IN '(1 -1 -1 1)
	    FOR SY IN '(1 1 -1 -1)
	    DO
	;; Draw a hyperbola in one quadrant.  X-MIN, Y-MIN, X-MAX, DX are in data
	;; coordinates.  Multiply by KX and KY to get window coordinates.
	(LET ((KX (* KX SX))
	      (KY (* KY SY))
	      (X-CENTER (+ X-CENTER (* SX OFFSET)))
	      (Y-CENTER (+ Y-CENTER (* SY OFFSET))))
	  (LOOP FOR X FROM X-MIN BELOW X-MAX BY DX
		DO
	    (DRAW-LINE-RELATIVE X-CENTER Y-CENTER
				(FIX (* KX (- X X-MIN)))
				(FIX (* KY (- (// 1 X) Y-MIN))))))))))

;; Draw an ellipse, or a circle if EQUALIZE? is T.

(DEFUN ELLIPSE (&OPTIONAL (EXTENT-FRACTION 0.8) (EQUALIZE? NIL) (D 0.01s0) (ALU TV:ALU-XOR))
  (MULTIPLE-VALUE-BIND (X-CENTER Y-CENTER X-EXTENT Y-EXTENT)
      (WINDOW-INFO-STATIC EXTENT-FRACTION EQUALIZE?)
    (LOOP FOR  FROM 0 BELOW 2 BY D
	  DO
      (DRAW-LINE-RELATIVE X-CENTER Y-CENTER
			  (FIX (* (COS ) X-EXTENT))
			  (FIX (* (SIN ) Y-EXTENT))
			  ALU))))

;; Nails and string.
;; N is the number of lines to draw per quadrant.
;; OFFSET is a number in pixels which is a margin between each quadrant and the center.

(DEFUN WEB (&OPTIONAL (EXTENT-FRACTION 0.9) (EQUALIZE? NIL) (N 100) (OFFSET 3))
  (MULTIPLE-VALUE-BIND (X-CENTER Y-CENTER X-EXTENT Y-EXTENT)
      (WINDOW-INFO-STATIC EXTENT-FRACTION EQUALIZE? OFFSET)
    (LOOP WITH DF = (// 1.0 N)
	  FOR SX IN '(1 -1 -1 1)
	  FOR SY IN '(1 1 -1 -1)
	  DO
      (LET ((X-EXTENT (* X-EXTENT SX))
	    (Y-EXTENT (* Y-EXTENT SY))
	    (X-CENTER (+ X-CENTER (* SX OFFSET)))
	    (Y-CENTER (+ Y-CENTER (* SY OFFSET))))
	(LOOP FOR F FROM 0s0 TO 1s0 BY DF
	      FOR G = (- 1s0 F)
	      DO
	  (DRAW-LINE (+ X-CENTER (FIX (* F X-EXTENT)))
		     Y-CENTER
		     X-CENTER
		     (+ Y-CENTER (FIX (* G Y-EXTENT)))))))))

;; Tunnel of rotating boxes
;; A is the incremental angle to rotate each box.
;; B is the cumulative angle.

(DEFUN TUNNEL (&OPTIONAL (EXTENT-FRACTION 0.9) (A 0.04s0) (N 150.))
  (MULTIPLE-VALUE-BIND (X-CENTER Y-CENTER EXTENT)
      (WINDOW-INFO-STATIC EXTENT-FRACTION T)
    (LOOP WITH SIN-A = (SIN A)
	  WITH COS-A = (COS A)	  
	  REPEAT N
	  FOR L = (* 2 EXTENT) THEN (// L (+ COS-A SIN-A))
	  FOR M = (* L SIN-A)
	  FOR X = (- X-CENTER EXTENT) THEN (+ X (* M SIN-B))
	  FOR Y = (+ Y-CENTER EXTENT) THEN (- Y (* M COS-B))
	  FOR B FROM 0s0 BY A
	  FOR SIN-B = (SIN B)
	  FOR COS-B = (COS B)
	  DO
      (TUNNEL-BOX X Y SIN-B COS-B L))))

(DEFUN TUNNEL-BOX (X1 Y1 SIN-B COS-B L)
  (SETQ X1 (FIX X1) Y1 (FIX Y1))
  (LET* ((L-SIN-B (FIX (* L SIN-B)))
	 (L-COS-B (FIX (* L COS-B)))
	 (X2 (+ X1 L-SIN-B))
	 (Y2 (- Y1 L-COS-B))
	 (X3 (+ X2 L-COS-B))
	 (Y3 (+ Y2 L-SIN-B))
	 (X4 (- X3 L-SIN-B))
	 (Y4 (+ Y3 L-COS-B)))
    (DRAW-LINES TV:ALU-IOR X1 Y1 X2 Y2 X3 Y3 X4 Y4 X1 Y1)))

;; Take an initial polygon and a final polygon and draw intermediate polygons which are
;; linear interpolations of these two, i.e. "warp" the initial polygon into the final one
;; linearly.  This is a basic function which many others call.  Note that Cycle is just
;; Warp-Internal with the initial and final polygons being identical but rotated.

(DEFUN WARP-INTERNAL (INITIAL-POLYGON FINAL-POLYGON N-STEPS)
  (LET ((N-POINTS (FILL-POINTER INITIAL-POLYGON)))
    (USING-POINT-VECTOR (DELTA-VECTOR N-POINTS)
      ;; Generate the delta vector by computing the vector difference of the polygons.
      (POINT-VECTOR-DIFFERENCE DELTA-VECTOR FINAL-POLYGON INITIAL-POLYGON)
      ;; Draw the thing, after shifting initial polygon to fit within window.
      (INCF N-STEPS)
      (LOOP FOR STEP FROM 0 BELOW N-STEPS
	    DO
	(LOOP FOR POINT FROM 0 BELOW N-POINTS
	      FOR POINT+1 FROM 1
	      DO
	  (IF (= POINT+1 N-POINTS) (SETQ POINT+1 0))
	  (DRAW-LINE (+ (XREF INITIAL-POLYGON POINT)
			(// (* (XREF DELTA-VECTOR POINT) STEP) N-STEPS))
		     (+ (YREF INITIAL-POLYGON POINT)
			(// (* (YREF DELTA-VECTOR POINT) STEP) N-STEPS))
		     (+ (XREF INITIAL-POLYGON POINT+1)
			(// (* (XREF DELTA-VECTOR POINT+1) STEP) N-STEPS))
		     (+ (YREF INITIAL-POLYGON POINT+1)
			(// (* (YREF DELTA-VECTOR POINT+1) STEP) N-STEPS))
		     ))))))

;; Generate two random polygons with the same number of sides and pass them to
;; Warp-Internal.  The number of warp steps is a function of the area of the window.

(DEFUN WARP (&OPTIONAL (MIN-POINTS 3) (RANDOM-POINTS 2) (MIN-STEPS 15.)
	     (AREA-PER-STEP 10000.) (AREA-PER-RANDOM-STEP 10000.))
  (MULTIPLE-VALUE-BIND (WIDTH HEIGHT AREA)
      (WINDOW-INFO-DYNAMIC)
    (LET ((N-POINTS (+ MIN-POINTS (RANDOM RANDOM-POINTS)))
	  (N-STEPS (+ MIN-STEPS (// AREA AREA-PER-STEP)
		      (RANDOM (1+ (// AREA AREA-PER-RANDOM-STEP))))))
      (USING-POINT-VECTOR (INITIAL-POLYGON N-POINTS)
	(USING-POINT-VECTOR (FINAL-POLYGON N-POINTS)
	  (POINT-VECTOR-RANDOMIZE INITIAL-POLYGON WIDTH HEIGHT)
	  (POINT-VECTOR-RANDOMIZE FINAL-POLYGON WIDTH HEIGHT)
	  (WARP-INTERNAL INITIAL-POLYGON FINAL-POLYGON N-STEPS))))))

;; Take a polygon and shift the endpoints along the edges.  Takes a vector of points in
;; two-space and a number of steps and cycles through the point vector.

(DEFUN CYCLE-INTERNAL (INITIAL-POLYGON N-STEPS)
  (USING-POINT-VECTOR (FINAL-POLYGON (FILL-POINTER INITIAL-POLYGON))
    (POINT-VECTOR-ROTATE FINAL-POLYGON INITIAL-POLYGON)
    (WARP-INTERNAL INITIAL-POLYGON FINAL-POLYGON N-STEPS)))

;; Generate a random polygon and pass it to Cycle-Internal.  The number of steps is a
;; function of the area of the window.

(DEFUN CYCLE ()
  (MULTIPLE-VALUE-BIND (WIDTH HEIGHT AREA)
      (WINDOW-INFO-DYNAMIC)
    (LET ((N-POINTS (+ 3 (RANDOM 6)))
	  (N-STEPS (+ 5 (// AREA 50000.) (RANDOM (1+ (// AREA 20000.))))))
      (USING-POINT-VECTOR (V N-POINTS)
	(POINT-VECTOR-RANDOMIZE V WIDTH HEIGHT)
	(CYCLE-INTERNAL V N-STEPS)))))

;; Construct a regular polygon out of several triangular cycles.  At least three
;; triangular cycles must be used.

(DEFUN POLYGON (&OPTIONAL (EXTENT-FRACTION 0.9) (EQUALIZE? T)
		(N-SEGMENTS 9) (N-STEPS 40.) (-0 //2))
  (LET ((D (// 2 N-SEGMENTS)))
    (MULTIPLE-VALUE-BIND (X-CENTER Y-CENTER X-EXTENT Y-EXTENT)
	(WINDOW-INFO-STATIC EXTENT-FRACTION EQUALIZE?)
      (USING-POINT-VECTOR (V 3)
	;; Establish the center of the polygon
	(XSET X-CENTER V 0)
	(YSET Y-CENTER V 0)
	(LOOP FOR I FROM 0 BELOW N-SEGMENTS
	      FOR -1 = (+ -0 (* I D))
	      FOR -2 = (+ -1 D)
	      DO
	  (XSET (+ X-CENTER (FIX (* X-EXTENT (COS -1)))) V 1)
	  (YSET (+ Y-CENTER (FIX (* Y-EXTENT (SIN -1)))) V 1)
	  (XSET (+ X-CENTER (FIX (* X-EXTENT (COS -2)))) V 2)
	  (YSET (+ Y-CENTER (FIX (* Y-EXTENT (SIN -2)))) V 2)
	  (CYCLE-INTERNAL V N-STEPS))))))

;; Inspired by a program by John Gonzalez, which was inspired by a program on
;; the PERQ, which was inspired by a video game called "Qix".

;; Use the AREA value returned by WINDOW-INFO-DYNAMIC later to compute a good
;; setting for DW (the line density).

(DEFMACRO QUIX-INCF (X DX LIMIT)
  `(PROGN (INCF ,X ,DX)
	  (COND ((NOT ( 0 ,X ,LIMIT))
		 (SETQ ,DX (- ,DX))
		 (INCF ,X (* 2 ,DX))))))

(DEFMACRO QUIX-RANDOM () '(* DW (- (RANDOM RANDOMNESS) HALF-RANDOMNESS)))

;; RANDOMNESS should be an odd number so that the numbers are centered around zero.

(DEFUN QUIX (&OPTIONAL (N-RANDOM 200) (N-POINTS 1_7)
	     (DW 5) (RANDOMNESS 5) (ALU TV:ALU-XOR))
  (MULTIPLE-VALUE-BIND (WIDTH HEIGHT IGNORE)
      (WINDOW-INFO-DYNAMIC)
    (USING-POINT-VECTOR (V0)
      (USING-POINT-VECTOR (V1)
	(LET ((X0 0) (X1 0) (Y0 0) (Y1 (1- HEIGHT))
	      (DX0 DW) (DX1 DW) (DY0 DW) (DY1 (- DW))
	      (HALF-RANDOMNESS (// RANDOMNESS 2)))
	  (LOOP FOR I-POINTS FROM 0
		FOR I-RANDOM FROM 0
		FOR I-TOTAL  FROM 0
		DO
	    (IF (= I-POINTS N-POINTS) (SETQ I-POINTS 0))
	    (COND ((= I-RANDOM N-RANDOM)
		   (SETQ I-RANDOM 0)
		   (SETQ DX0 (QUIX-RANDOM))
		   (SETQ DX1 (QUIX-RANDOM))
		   (SETQ DY0 (QUIX-RANDOM))
		   (SETQ DY1 (QUIX-RANDOM))))
	    (QUIX-INCF X0 DX0 WIDTH)
	    (QUIX-INCF X1 DX1 WIDTH)
	    (QUIX-INCF Y0 DY0 HEIGHT)
	    (QUIX-INCF Y1 DY1 HEIGHT)
	    (DRAW-LINE X0 Y0 X1 Y1 ALU)
	    (IF ( I-TOTAL N-POINTS)
		(DRAW-LINE (XREF V0 I-POINTS) (YREF V0 I-POINTS)
			   (XREF V1 I-POINTS) (YREF V1 I-POINTS)
			   ALU))
	    (XSET X0 V0 I-POINTS)
	    (YSET Y0 V0 I-POINTS)
	    (XSET X1 V1 I-POINTS)
	    (YSET Y1 V1 I-POINTS)
	    ))))))

;; Everything above this point works on arbitrary streams.
;; This is a simple front-end for serial streams.  After this is a front-end
;; for the window system.  We could use :CURSOR-OFF and :CURSOR-ON messages here.

(DEFCONST *PICTURE-COMMAND-ALIST*
	  '((#\B WEB "Web")
	    (#\C CYCLE "Cycle")
	    (#\E ELLIPSE "Ellipse")
	    (#\H HYPERBOLA "Hyperbola")
	    (#\P POLYGON "Polygon")
	    (#\Q QUIX "Quix")
	    (#\T TUNNEL "Tunnel")
	    (#\W WARP "Warp")
	    ))

(DEFUN PICTURE-HELP ()
  (SEND TERMINAL-IO :CLEAR-WINDOW)
  (LOOP WITH WIDTH = (SEND TERMINAL-IO :SIZE-IN-CHARACTERS)
	FOR XPOS = (SEND TERMINAL-IO :READ-CURSORPOS :CHARACTER)
	FOR LIST ON *PICTURE-COMMAND-ALIST*
	DO
    (DESTRUCTURING-BIND (CHAR IGNORE NAME) (CAR LIST)
      (IF ( (+ XPOS 6 (STRING-LENGTH NAME)) WIDTH)
	  (SEND TERMINAL-IO :TYO #\RETURN))
      (FORMAT T "~C = ~A~:[~;, ~]" CHAR NAME (CDR LIST))))
  (FORMAT T "~%Type c-‘ to abort out of things which run indefinitely.~@
	       Type ” to exit.~%"))

(DEFUN PICTURE ()
  (LET (CHAR (THROWN? T))
    (ERROR-RESTART-LOOP ((ERROR SYS:ABORT) "Picture Top Level~@[ in ~A~]"
			 (SEND TERMINAL-IO :SEND-IF-HANDLES :NAME))
      (IF THROWN? (PICTURE-HELP))
      (SETQ THROWN? T)
      (SETQ CHAR (SEND TERMINAL-IO :TYI))
      (SELECTOR CHAR CHAR-EQUAL
	((#\HELP #\SPACE)
	 (PICTURE-HELP))
	(#\END
	 (SEND TERMINAL-IO :CLEAR-WINDOW)
	 (RETURN (VALUES)))
	(OTHERWISE
	 (SETQ CHAR (ASSQ (CHAR-UPCASE CHAR) *PICTURE-COMMAND-ALIST*))
	 (COND ((NOT CHAR)
		(BEEP)
		(PICTURE-HELP))
	       (T (SEND TERMINAL-IO :CLEAR-WINDOW)
		  (FUNCALL (CADR CHAR))))))
      (SETQ THROWN? NIL)
      )))

;; There are three types of pictures:
;;   Static -- A picture which does not change with time and which will be drawn
;;     the same way every time.  No randomness is involved in the drawing at all.
;;   Dynamic -- A picture which does not change with time, but which may be
;;     drawn differently each time.
;;   Continuous -- A picture which changes with time.

;; All picture windows have processes associated with them, even static pictures,
;; so that the windows can be selected and typed characters have a place to go.

(DEFVAR *PICTURE-SAVE-BITS-DEFAULT* T)

(DEFFLAVOR PICTURE-WINDOW (GENERATOR) (TV:PROCESS-MIXIN TV:WINDOW)
  (:INITABLE-INSTANCE-VARIABLES GENERATOR)
  (:DEFAULT-INIT-PLIST :LABEL NIL :BLINKER-P NIL :SAVE-BITS *PICTURE-SAVE-BITS-DEFAULT*))

;; Static and dynamic pictures are assumed to only take a short time to draw, and so are
;; drawn with interrupts masked.  This only speeds things up by 10%, but the picture is
;; drawn more smoothly.

(DEFMETHOD (:DRAW-PICTURE PICTURE-WINDOW) ()
  (TV:PREPARE-SHEET (SELF) (FUNCALL GENERATOR)))

;; If bits aren't being saved, redraw the picture when exposed.  Also redraw the picture
;; when Function Refresh is typed.  Restart the generator rather than jumping back into
;; the middle when the window is exposed.  The first thing the overseer does is redraw the
;; window.

(DEFMETHOD (:REFRESH PICTURE-WINDOW :AFTER) (&REST IGNORE)
  (OR TV:RESTORED-BITS-P (SEND TV:PROCESS :RESET)))

;; Toggle between IOR and XOR line drawing modes.

(DEFMETHOD (:TOGGLE-XOR PICTURE-WINDOW) ()
  (SETQ TV:CHAR-ALUF (IF (= TV:CHAR-ALUF TV:ALU-XOR) TV:ALU-IOR TV:ALU-XOR)))

(DEFFLAVOR STATIC-PICTURE-WINDOW () (PICTURE-WINDOW)
  (:DEFAULT-INIT-PLIST :PROCESS '(STATIC-PICTURE-OVERSEER)))

(DEFFLAVOR DYNAMIC-PICTURE-WINDOW () (PICTURE-WINDOW)
  (:DEFAULT-INIT-PLIST :PROCESS '(DYNAMIC-PICTURE-OVERSEER)))

(DEFFLAVOR CONTINUOUS-PICTURE-WINDOW () (PICTURE-WINDOW)
  (:DEFAULT-INIT-PLIST :PROCESS '(CONTINUOUS-PICTURE-OVERSEER)))

(DEFMETHOD (:DRAW-PICTURE CONTINUOUS-PICTURE-WINDOW) () (FUNCALL GENERATOR))

;; The process associated with a picture window.  The picture is drawn when the window is
;; first exposed, so we sleep first.  Call the parameter TERMINAL-IO rather than WINDOW so
;; that when C-Abort is typed, there is some place to print the "[Abort]".

(DEFVAR *PICTURE-SLEEP-TIME* 180.)

(DEFVAR *PICTURE-HELP*
	"C		Complement the bits on this window~@
	 X		Complement XOR writing mode~@
      ~@[Space		Draw the next image~@
	 Ÿ		Freeze this image~@
       ~]”		Exit from this program~@
	 ~%Type any character to continue: ")

(DEFUN PICTURE-OVERSEER (TERMINAL-IO DYNAMIC?)
  (CONDITION-CASE ()
      (PROGN
	(SEND TERMINAL-IO :DRAW-PICTURE)
	(DO () (NIL)
	  (PROCESS-WAIT-WITH-TIMEOUT "Sleep" (AND DYNAMIC? *PICTURE-SLEEP-TIME*)
	    #'SEND TERMINAL-IO :LISTEN)
	  (WHEN (OR (NOT (SEND TERMINAL-IO :LISTEN))
		    (SELECTOR (CHAR-UPCASE (SEND TERMINAL-IO :TYI)) CHAR-EQUAL
		      (#\HELP
		       (SEND TERMINAL-IO :CLEAR-WINDOW)
		       (FORMAT TERMINAL-IO *PICTURE-HELP* DYNAMIC?)
		       (PICTURE-TYI-SPACE)
		       T)
		      (#\ESCAPE
		       (WHEN DYNAMIC?
			 (PROCESS-WAIT "Hold Picture" TERMINAL-IO :LISTEN)
			 (PICTURE-TYI-SPACE))
		       T)
		      ((#\END #/Q)
		       (SIGNAL 'SYS:ABORT))
		      (#/C (SEND TERMINAL-IO :SET-REVERSE-VIDEO-P
				 (NOT (SEND TERMINAL-IO :REVERSE-VIDEO-P)))
		       NIL)
		      (#/X (SEND TERMINAL-IO :TOGGLE-XOR)
		       NIL)
		      (T T)))
	    (SEND TERMINAL-IO :CLEAR-WINDOW)
	    (SEND TERMINAL-IO :DRAW-PICTURE))))
    (SYS:ABORT (SEND TERMINAL-IO :DESELECT))))

(DEFUN PICTURE-TYI-SPACE ()
  (LET ((CHAR (SEND TERMINAL-IO :TYI)))
    (OR (CHAR-EQUAL CHAR #\SPACE) (SEND TERMINAL-IO :UNTYI CHAR))))

(DEFUN STATIC-PICTURE-OVERSEER (WINDOW) (PICTURE-OVERSEER WINDOW NIL))

(DEFUN DYNAMIC-PICTURE-OVERSEER (WINDOW) (PICTURE-OVERSEER WINDOW T))

;; The overseer for a continuous picture is considerably simpler, since
;; we can't transmit any synchronous commands.  Later, more asynchronous commands
;; can be added.

(DEFUN CONTINUOUS-PICTURE-OVERSEER (TERMINAL-IO)
  (OR (SEND TERMINAL-IO :ASYNCHRONOUS-CHARACTER-P #\END)
      (SEND TERMINAL-IO :ADD-ASYNCHRONOUS-CHARACTER #\END
	    #'(LAMBDA (IGNORE IGNORE)
		(TV:KBD-ASYNCHRONOUS-INTERCEPT-CHARACTER #\C-ABORT))))
  (CONDITION-CASE ()
      (SEND TERMINAL-IO :DRAW-PICTURE)
    (SYS:ABORT (SEND TERMINAL-IO :DESELECT))))

;; Add a "Picture" option to the system menu.  This goes to a second menu to determine the
;; type of picture to create.  The window resource hair is needed to make this work
;; correctly on multiple screens.

(DEFCONST *PICTURE-ITEM-LIST*
	  '(("Warp"	 :VALUE WARP-WINDOW)
	    ("Quix"	 :VALUE QUIX-WINDOW)
	    ("Cycle"	 :VALUE CYCLE-WINDOW)
	    ("Polygon"   :VALUE POLYGON-WINDOW)
	    ("Tunnel"	 :VALUE TUNNEL-WINDOW)
	    ("Hyperbola" :VALUE HYPERBOLA-WINDOW)
	    ("Web"	 :VALUE WEB-WINDOW)))

(TV:DEFWINDOW-RESOURCE PICTURE-MENU ()
  :MAKE-WINDOW (TV:MOMENTARY-MENU :ITEM-LIST *PICTURE-ITEM-LIST*)
  :REUSABLE-WHEN :DEEXPOSED)

(DEFUN CHOOSE-FROM-PICTURE-MENU () (SEND (ALLOCATE-RESOURCE 'PICTURE-MENU) :CHOOSE))

(DEFUN PICTURE-SETUP ()
  (UNLESS (ASSOC "Picture" TV:DEFAULT-WINDOW-TYPES-ITEM-LIST)
    (PUSH '("Picture"
	    :FUNCALL CHOOSE-FROM-PICTURE-MENU
	    :DOCUMENTATION "Various graphics hacks.")
	  TV:DEFAULT-WINDOW-TYPES-ITEM-LIST)
    T))

(DEFFLAVOR HYPERBOLA-WINDOW () (STATIC-PICTURE-WINDOW)
  (:DEFAULT-INIT-PLIST :GENERATOR #'HYPERBOLA))

(DEFFLAVOR WEB-WINDOW () (STATIC-PICTURE-WINDOW)
  (:DEFAULT-INIT-PLIST :GENERATOR #'WEB))

(DEFFLAVOR POLYGON-WINDOW () (STATIC-PICTURE-WINDOW)
  (:DEFAULT-INIT-PLIST :GENERATOR #'POLYGON))

(DEFFLAVOR TUNNEL-WINDOW () (STATIC-PICTURE-WINDOW)
  (:DEFAULT-INIT-PLIST :GENERATOR #'TUNNEL))

(DEFFLAVOR WARP-WINDOW () (DYNAMIC-PICTURE-WINDOW)
  (:DEFAULT-INIT-PLIST :GENERATOR #'WARP))

(DEFFLAVOR CYCLE-WINDOW () (DYNAMIC-PICTURE-WINDOW)
  (:DEFAULT-INIT-PLIST :GENERATOR #'CYCLE))

(DEFFLAVOR QUIX-WINDOW () (CONTINUOUS-PICTURE-WINDOW)
  (:DEFAULT-INIT-PLIST :GENERATOR #'QUIX))

(COMPILE-FLAVOR-METHODS HYPERBOLA-WINDOW WEB-WINDOW POLYGON-WINDOW TUNNEL-WINDOW
			WARP-WINDOW CYCLE-WINDOW QUIX-WINDOW)

;; To do:		

;; Add PERQ petal program.
;; Make this stuff keyword driven?
;; Try dynamically changing ALU function.  Try this in color.
;; Get rid of all of the different flavors of window.  Static, Dynamic, and Continuous
;;  alone should be sufficient, and maybe not even that.
